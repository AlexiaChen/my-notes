## 通用哲学观

### 为失败作好准备

任何非琐碎(non-trivial)的合约都会有错误。因此，你的代码必须能够优雅地应对错误和漏洞。

- 当事情出错时暂停合约（"断路器（ciruit breaker）"）。
- 管理风险金额（费率限制，最大使用量）。
- 有一个有效的升级路径来修复错误和改进问题

### 保持更新

追踪新的安全发展。

- 一旦发现任何新的错误，立即检查你的合约
- 尽快升级到任何工具或库的最新版本
- 采用看起来有用的新安全技术

### 保持简单

复杂性会增加出错的可能性。

- 确保合约逻辑简单
- 将代码模块化，使合约和函数保持小规模
- 尽可能使用已经写好的工具或代码（例如，不要自己开发随机数发生器）。
- 在可能的情况下，优先考虑清晰性而不是性能
- 只在系统中需要去中心化的部分使用区块链。

### 发布流程

在完整的生产发布之前抓住错误总是更好的。

- 彻底测试合约，并在发现新的攻击载体时增加测试。
- 从alpha测试网发布开始，提供bug悬赏。
- 分阶段推出，在每个阶段增加使用和测试。

### 区块链属性

虽然你的许多编程经验将与以太坊编程有关，但也有一些陷阱需要注意。

- 对外部合约的调用要非常小心，这可能会执行恶意代码并改变控制流。
- 要明白你的公共函数是公开的，可能被恶意调用，而且是以任何顺序调用。智能合约中的私人数据也可以被任何人查看。
- 牢记gas成本和区块gas限制。
- 要注意时间戳在区块链上是不精确的，矿工可以在几秒的范围内影响交易的执行时间。
- 随机性在区块链上是不可行的，大多数随机数生成的方法在区块链上都是可以玩的。

### 简单和复杂

在评估智能合约系统的结构和安全性时，有多种基本的权衡需要考虑。对任何智能合约系统的一般建议是确定这些基本权衡的适当平衡。

从软件工程的角度来看，一个理想的智能合约系统是模块化的，重用代码而不是重复代码，并支持可升级的组件。从安全架构的角度来看，一个理想的智能合约系统可能会有这样的心态，特别是在更复杂的智能合约系统的情况下。

然而，也有一些重要的例外情况，安全和软件工程的最佳实践可能不一致。在每一种情况下，适当的平衡是通过沿着合约系统的维度确定属性的最佳组合来获得的，例如以下讨论的话题

#### 刚性和可升级型

虽然包括本网站在内的多种资源都强调了可塑性特征，如可杀伤、可升级或可修改的模式，但在可塑性和安全性之间存在着基本的权衡。

根据定义，可塑性模式增加了复杂性和潜在的攻击面。在智能合约系统在预先定义的有限时间内执行一套非常有限的功能的情况下，简单性比复杂性特别有效，例如，一个无治理的有限时间框架的代币销售合约系统。

#### 单体化和模块化

一个单体的独立合约可以保持所有知识的本地可识别性和可读性。虽然很少有被高度重视的智能合约系统是以单体形式存在的，但也有一种说法是数据和流程的极端本地化--例如，在优化代码审查效率的情况下。

与这里考虑的其他权衡一样，在简单的短期合约中，安全的最佳实践趋于远离软件工程的最佳实践，而在更复杂的永久合约系统中，则趋于软件工程的最佳实践。

> 也就是代码不复杂的话，就不要老是想着模块化了，反过来一样。

#### 重复和复用

从软件工程的角度来看，一个智能合约系统希望在合理的情况下最大限度地重复使用。在Solidity中，有很多方法可以重用合约代码。使用你自己拥有的经过验证的先前部署的合约，通常是实现代码重用的最安全方式。

在自己拥有的先前部署的合约不可用的情况下，经常依靠重复使用。像OpenZeppelin的Solidity库这样的努力试图提供模式，使安全的代码可以被重复使用而不需要重复。任何合约安全分析必须包括任何重新使用的代码，这些代码以前没有建立与目标智能合约系统中的风险资金相称的信任水平。

## 开发建议

### 常规建议

#### 外部调用

##### 1. 进行外部调用的时候要小心

对不受信任的合约的调用会带来一些意想不到的风险或错误。外部调用可能会在该合约或它所依赖的任何其他合约中执行恶意代码。因此，每一个外部调用都应该被视为一个潜在的安全风险。当不可能，或不希望删除外部调用时，使用本节其余部分的建议，以尽量减少危险。

##### 2. 对不受信的合约进行标记

当与外部合约交互时，以一种明确的方式命名你的变量、方法和合约接口，表明与它们交互是潜在的不安全的。这也适用于你自己调用外部合约的函数。

```d
// bad
Bank.withdraw(100); // Unclear whether trusted or untrusted

function makeWithdrawal(uint amount) { // Isn't clear that this function is potentially unsafe
    Bank.withdraw(amount);
}

// good
UntrustedBank.withdraw(100); // untrusted external call
TrustedBank.withdraw(100); // external but trusted bank contract maintained by XYZ Corp

function makeUntrustedWithdrawal(uint amount) {
    UntrustedBank.withdraw(amount);
}
```

##### 3. 避免外部调用后的状态变化

无论是使用原始调用（形式为`someAddress.call()`）还是合约调用（形式为`ExternalContract.someMethod()`），都要假设恶意代码可能会执行。即使`ExternalContract`不是恶意的，恶意代码也会被它所调用的任何合约执行。

一个特别的危险是，恶意代码可能会劫持控制流，导致由于重入而产生的漏洞。(关于这个问题的更全面的讨论，请参见重入性）。

如果你正在调用一个不受信任的外部合约，请避免在调用后进行状态改变。这种模式有时也被称为检查-效果-互动模式(Checks-Effects-Interactions Pattern)。[Security Considerations — Solidity 0.8.18 documentation (soliditylang.org)](https://docs.soliditylang.org/en/develop/security-considerations.html#use-the-checks-effects-interactions-pattern) 

[SWC-107 · Overview (swcregistry.io)](https://swcregistry.io/docs/SWC-107)

##### 4. 不要使用`transfer()`或者`send()`

`.transfer()`和`.send()`正好将2300个gas转发给接收者。这种硬编码的气体津贴的目的是为了防止重入漏洞，但这只有在gas成本不变的假设下才有意义。最近，[EIP 1884]([EIP-1884: Repricing for trie-size-dependent opcodes (ethereum.org)](https://eips.ethereum.org/EIPS/eip-1884))被列入伊斯坦布尔硬分叉中。EIP 1884中包含的一个变化是增加了`SLOAD`操作的gas成本，导致合约的fallback功能成本超过2300 gas。

建议停止使用`.transfer()`和`.send()``，而是使用`.call()`。

```d
// bad
contract Vulnerable {
    function withdraw(uint256 amount) external {
        // This forwards 2300 gas, which may not be enough if the recipient
        // is a contract and gas costs change.
        msg.sender.transfer(amount);
    }
}

// good
contract Fixed {
    function withdraw(uint256 amount) external {
        // This forwards all available gas. Be sure to check the return value!
        (bool success, ) = msg.sender.call.value(amount)("");
        require(success, "Transfer failed.");
    }
}
```

请注意，`.call()`对减轻重入攻击毫无作用，所以必须采取其他预防措施。为了防止重入攻击。请用Checks-Effects-Interactions Pattern。

##### 5. 处理外部调用的错误

Solidity 提供了对原始地址工作的低级调用方法：`address.call()`, `address.callcode()`, `address.delegatecall()`, 和 `address.send()`。这些低级方法从不抛出异常，但如果调用遇到异常，将返回`false`。另一方面，合约调用（例如`ExternalContract.doSomething()``）将自动传播一个throw（例如，如果`doSomething()`抛出，ExternalContract.doSomething()也将抛出）。

如果你选择使用低级别的调用方法，请确保通过检查返回值来处理调用失败的可能性。

```d
// bad
someAddress.send(55);
someAddress.call.value(55)(""); // this is doubly dangerous, as it will forward all remaining gas and doesn't check for result
someAddress.call.value(100)(bytes4(sha3("deposit()"))); // if deposit throws an exception, the raw call() will only return false and transaction will NOT be reverted

// good
(bool success, ) = someAddress.call.value(55)("");
if(!success) {
    // handle failure code
}

ExternalContract(someAddress).deposit.value(100)();
```

[SWC-104 · Overview (swcregistry.io)](https://swcregistry.io/docs/SWC-104) 

##### 6. 倾向pull而不是push外部调用

外部调用可能意外地或故意地失败。为了尽量减少这种失败造成的损失，通常最好是把每个外部调用隔离成自己的交易，可以由调用的接收者发起。这对于支付来说尤其重要，最好是让用户提取(pull)资金，而不是自动推送(push)资金给他们。(这也减少了gas限制出现问题的机会 [Denial of Service - Ethereum Smart Contract Best Practices (consensys.github.io)](https://consensys.github.io/smart-contract-best-practices/attacks/denial-of-service/) ) 避免在一个交易中合并多个以太币转移。

```d
// bad
contract auction {
    address highestBidder;
    uint highestBid;

    function bid() payable {
        require(msg.value >= highestBid);

        if (highestBidder != address(0)) {
            (bool success, ) = highestBidder.call.value(highestBid)("");
            require(success); // if this call consistently fails, no one else can bid
        }

       highestBidder = msg.sender;
       highestBid = msg.value;
    }
}

// good
contract auction {
    address highestBidder;
    uint highestBid;
    mapping(address => uint) refunds;

    function bid() payable external {
        require(msg.value >= highestBid);

        if (highestBidder != address(0)) {
            refunds[highestBidder] += highestBid; // record the refund that this user can claim
        }

        highestBidder = msg.sender;
        highestBid = msg.value;
    }

    function withdrawRefund() external {
        uint refund = refunds[msg.sender];
        refunds[msg.sender] = 0;
        (bool success, ) = msg.sender.call.value(refund)("");
        require(success);
    }
}

```

> 看上面的代码，我的理解就是说，这样对于一个外部调用，要有容错，这个容错的方式就是让用户来主动调用，给用户看到报错。

[SWC-128 · Overview (swcregistry.io)](https://swcregistry.io/docs/SWC-128)

##### 7. 不要delegatecall不受信的代码

`delegatecall`函数被用来调用其他合约的函数，就像它们属于caller合约一样。因此，callee可以改变调用地址的状态（callee的代码是在caller的状态上下文中运行的）。这可能是不安全的。下面的一个例子显示了使用`delegatecall`如何导致合约的破坏和失去平衡。

```d
contract Destructor
{
    function doWork() external
    {
        selfdestruct(0);
    }
}

contract Worker
{
    function doWork(address _internalWorker) public
    {
        // unsafe
        _internalWorker.delegatecall(bytes4(keccak256("doWork()")));
    }
}
```

如果`Worker.doWork()`被调用，并将部署的`Destructor`合约的地址作为参数，`Worker`合约将自毁。只将执行委托给受信任的合约，而不要委托给用户提供的地址。

> 警告： 不要假设合约是以零余额创建的。攻击者可以在合约创建之前向其地址发送以太币，所以合约不应该假设其初始状态包含零余额。更多细节见问题61。[Don't assume contracts are created with zero balance · Issue #61 · ConsenSys/smart-contract-best-practices (github.com)](https://github.com/ConsenSys/smart-contract-best-practices/issues/61) 

[SWC-112 · Overview (swcregistry.io)](https://swcregistry.io/docs/SWC-112)

#### 强行灌入以太币

小心编码一个严格检查合约余额的不变式。

攻击者可以强行向任何账户发送以太币，这是不可能被阻止的（即使有一个做`revert()`的fallback函数也不行）。

攻击者可以通过创建一个合约，用1wei为它提供资金，并调用`selfdestruct(victimAddress)`来做到这一点。在 `victimAddress` 中没有调用任何代码，所以无法阻止它。区块奖励也是如此，它被发送到矿工的地址，这可以是任何任意的地址。

此外，由于合约地址可以预先计算，在合约部署之前，以太币可以被发送到一个地址。

[SWC-132 · Overview (swcregistry.io)](https://swcregistry.io/docs/SWC-132)

#### 公共的链上数据

许多应用要求提交的数据在某个时间点之前都是私有的，以便工作。游戏（如链上剪刀石头布）和拍卖(Auction)机制（如密封竞价的Vickrey拍卖 [Vickrey auction - Wikipedia](https://en.wikipedia.org/wiki/Vickrey_auction) ）是两大类例子。如果你正在建立一个隐私问题的应用程序，请确保你避免要求用户过早地发布信息。最好的策略是使用具有独立几个阶段的承诺方案 [Commitment scheme - Wikipedia](https://en.wikipedia.org/wiki/Commitment_scheme) ：第一个阶段使用哈希值(hash（choose-value）)进行承诺，并在稍后的阶段reveal这些数值。  
  
例子：
  
- 在剪刀石头布游戏中，要求双方玩家先提交他们打算采取的行动的hash，然后要求双方玩家提交他们的行动；如果提交的行动与哈希值不一致，就把它扔掉。  
- 在拍卖中，要求玩家在初始阶段提交他们的出价的哈希值（以及大于他们的出价的保证金），然后在第二阶段提交他们的拍卖出价值。  
- 当开发一个依赖随机数生成器的应用程序时，其顺序应该总是：（1）玩家提交动作，（2）生成随机数，（3）玩家支付。产生随机数的方法本身就是一个活跃的研究领域；目前最好的解决方案包括比特币区块头（通过http://btcrelay.org 进行验证）、hash-commitment-reveal方案（即一方产生一个数字，公布其哈希值以 "承诺 "该值，然后随后reveal该值）和RANDAO [randao/randao: RANDAO: A DAO working as RNG of Ethereum (github.com)](https://github.com/randao/randao) 。由于以太坊是一个确定性的协议，协议内的任何变量都不能被用作不可预测的随机数。另外，要注意的是，矿工在某种程度上控制着block.blockhash()值。  [contract design - When can BLOCKHASH be safely used for a random number? When would it be unsafe? - Ethereum Stack Exchange](https://ethereum.stackexchange.com/questions/419/when-can-blockhash-be-safely-used-for-a-random-number-when-would-it-be-unsafe) 

更多请参考 [[ETH之随机性和RANDAO]]
  
#### 不可靠的参与者

不要让退款或索赔过程依赖于特定的一方执行特定的行动，而没有其他的方式将资金取出。例如，在石头剪刀布游戏中，一个常见的错误是在双方都提交自己的动作之前不进行赔付；然而，一个恶意的玩家可以通过简单地从不提交自己的动作来 "恶心 "对方--事实上，如果一个玩家看到其他玩家透露的动作并确定他们输了，他们根本就没有理由提交自己的动作。这个问题也可能出现在国家频道结算的背景下。当这种情况成为一个问题时，（1）提供一种规避不参与游戏的参与者的方法，也许是通过时间限制，以及（2）考虑增加经济激励，使参与者在所有他们应该提交信息的情况下提交信息。

#### 有符号整数的负数

Solidity 提供了几种类型来处理有符号的整数。像大多数编程语言一样，在Solidity中，一个有符号的N位整数可以代表从`-2^(N-1)`到`2^(N-1)-1`的值。这意味着`MIN_INT`没有正数的等价物。负数的实现是寻找一个数字的二进制补码，所以最负的数字的负数会产生相同的数字。[Two's complement - Wikipedia](https://en.wikipedia.org/wiki/Two%27s_complement#Most_negative_number) 

这对Solidity中所有的有符号整数类型（`int8`, `int16`, ..., `int256`）都是如此。

```d
contract Negation {
    function negate8(int8 _i) public pure returns(int8) {
        return -_i;
    }

    function negate16(int16 _i) public pure returns(int16) {
        return -_i;
    }

    int8 public a = negate8(-128); // -128
    int16 public b = negate16(-128); // 128
    int16 public c = negate16(-32768); // -32768
}
```

一种处理方法是在负数(Negation)前检查变量的值，如果等于`MIN_INT`就扔掉。另一个办法是通过使用容量更大的类型（例如`int32`而不是`int16`），确保永远不会出现最负的数字。

在`MIN_INT`乘以或除以`-1`时，会出现与`int`类型类似的问题。

### 预防措施

#### 常规措施

这里没什么，跟之前一节所说的差不多 [[#通用哲学观]]

#### 可升级性

> 注意： 智能合约的可升级性是一个活跃的研究领域。有许多重要的问题，以及与智能合约可升级性有关的风险。做好对技术现状的研究。我们欢迎对相关问题进行讨论。[Improve upgradeability section · Issue #164 · ConsenSys/smart-contract-best-practices (github.com)](https://github.com/ConsenSys/smart-contract-best-practices/issues/164) 

如果发现错误或需要改进，就需要修改代码。发现一个错误，但没有办法处理它是不好的。  
  
为智能合约设计一个有效的升级系统是一个积极研究的领域，我们无法在本文中涵盖所有的复杂情况。然而，有两种基本方法是最常用的。这两种方法中比较简单的是有一个注册合约，持有合约的最新版本的地址。对合约用户来说，一个更无缝的方法是有一个合约，将调用和数据转发到最新版本的合约上。  
  
无论采用什么技术，重要的是要有模块化和组件之间的良好分离，这样代码的改变就不会破坏功能，不会出现孤儿数据，也不会需要大量的成本来移植。特别是，将复杂的逻辑与你的数据存储分开通常是有益的，这样你就不必为了改变功能而重新创建所有的数据。  [[可升级合约#代理升级模式]]
  
有一个安全的方式让各方决定升级代码也很关键。根据你的合约，代码的改变可能需要由一个受信任的一方，一组成员，或全套利益相关者的投票来批准。如果这个过程可能需要一些时间，你要考虑是否有其他的方法在发生攻击时能更快地做出反应，如紧急停止或断路器。  
  
无论你的方法是什么，重要的是要有一些方法来升级你的合约，否则当合约中不可避免的错误被发现时，它们将变得无法使用。  

##### 1. 使用注册合约来存储合约的最新版本

在这个例子中，调用没有被转发，所以用户应该在每次与之互动之前获取当前地址。

```d
pragma solidity ^0.5.0;

contract SomeRegister {
    address backendContract;
    address[] previousBackends;
    address owner;

    constructor() {
        owner = msg.sender;
    }

    modifier onlyOwner() {
        require(msg.sender == owner)
        _;
    }

    function changeBackend(address newBackend) public
    onlyOwner()
    returns (bool)
    {
        if(newBackend != address(0) && newBackend != backendContract) {
            previousBackends.push(backendContract);
            backendContract = newBackend;
            return true;
        }

        return false;
    }
}

```

##### 2. 使用delegatecall来转发数据和调用

这种方法依赖于使用fallback函数（在`Relay`合约中），使用delegatecall将调用转发给目标合约（`LogicContract`）。记住，delegatecall https://solidity.readthedocs.io/en/latest/introduction-to-smart-contracts.html#delegatecall-callcode-and-libraries 是Solidity中的一个特殊函数，它在调用合约（Relay）的上下文中执行被调用地址（LogicContract）的逻辑，所以 "存储、当前地址和余额仍然指调用合约，只是代码取自被调用地址"。

```d
pragma solidity ^0.5.0;

contract Relay {
    address public currentVersion;
    address public owner;

    modifier onlyOwner() {
        require(msg.sender == owner);
        _;
    }

    constructor(address initAddr) {
        require(initAddr != address(0));
        currentVersion = initAddr;
        owner = msg.sender; // this owner may be another contract with multisig, not a single contract owner
    }

    function changeContract(address newVersion) public
    onlyOwner()
    {
        require(newVersion != address(0));
        currentVersion = newVersion;
    }

    fallback() external payable {
        (bool success, ) = address(currentVersion).delegatecall(msg.data);
        require(success);
    }
}

```


```d
contract LogicContract {
    address public currentVersion;
    address public owner;
    uint public counter;

    function incrementCounter() {
        counter++;
    }
}

```

这种模式的简单版本不能从`LogicContract`的函数中返回值，只能转发，这限制了它的适用性。更复杂的实现试图通过内联汇编代码和返回大小的注册表来解决这个问题。它们通常被称为代理模式 [[可升级合约#代理升级模式]] ，但也被称为Router [PeterBorah/ether-router: Upgradeable smart contracts on Ethereum (github.com)](https://github.com/PeterBorah/ether-router) 、Dispatcher https://gist.github.com/Arachnid/4ca9da48d51e23e5cfe0f0e14dd6318f 和Relay。每种实现方式都引入了一系列不同的复杂性、风险和限制。

你必须非常小心地对待你用这种方法存储数据的方式。如果你的新合约的存储布局与第一个合约不同，你的数据可能最终会被破坏。当使用`delegatecall`的更复杂的实现时，你应该仔细考虑和理解以下问题

-   How the EVM handles the [layout of state variables in storage](https://docs.soliditylang.org/en/latest/internals/layout_in_storage.html), including packing multiple variables into a single storage slot if possible
-   How and why [the order of inheritance](https://github.com/OpenZeppelin/openzeppelin-sdk/issues/37) impacts the storage layout
-   Why the called contract (`LogicContract`) must have the same storage layout of the calling contract (`Relay`), and only append new variables to the storage (see [Background on delegatecall](https://blog.trailofbits.com/2018/09/05/contract-upgrade-anti-patterns/))
-   Why a new version of the called contract (`LogicContract`) [must have the same storage layout as the previous version](https://github.com/OpenZeppelin/openzeppelin-sdk/issues/37), and only append new variables to the storage
-   [How a contract's constructor can affect upgradeability](https://blog.openzeppelin.com/towards-frictionless-upgradeability/)
-   How the ABI specifies [function selectors](https://solidity.readthedocs.io/en/v0.4.24/abi-spec.html?highlight=signature#function-selector) and how [function-name collision](https://medium.com/nomic-labs-blog/malicious-backdoors-in-ethereum-proxies-62629adf3357) can be used to exploit a contract that uses `delegatecall`
-   How `delegatecall` to a non-existent contract will return true even if the called contract does not exist. For more details see [Breaking the proxy pattern](https://blog.trailofbits.com/2018/09/05/contract-upgrade-anti-patterns/) and Solidity docs on [Error handling](https://solidity.readthedocs.io/en/latest/control-structures.html#error-handling-assert-require-revert-and-exceptions).
-   Remember the [importance of immutability to achieve trustlessness](https://consensys.net/diligence/blog/2019/01/upgradeability-is-a-bug/)

[Contract upgrade anti-patterns | Trail of Bits Blog](https://blog.trailofbits.com/2018/09/05/contract-upgrade-anti-patterns/) 

> 其实小节的本意就是编写可升级合约要注意的地方罢了 [[可升级合约#编写可升级合约]]

#### 断路器

断路器在满足某些条件的情况下停止执行，在发现新的错误时，断路器可以发挥作用。例如，如果发现了一个错误，合约中的大多数行动可能被暂停，现在唯一活跃的行动是撤回。你可以给某些受信任的人以触发断路器的能力，否则就有程序化的规则，在满足某些条件时自动触发某些断路器。

```d
bool private stopped = false;
address private owner;

modifier isAdmin() {
    require(msg.sender == owner);
    _;
}

function toggleContractActive() isAdmin public {
    // You can add an additional modifier that restricts stopping a contract to be based on another action, such as a vote of users
    stopped = !stopped;
}

modifier stopInEmergency { if (!stopped) _; }
modifier onlyInEmergency { if (stopped) _; }

function deposit() stopInEmergency public {
    // some code
}

function withdraw() onlyInEmergency public {
    // some code
}
```

> 以上代码本质就是一个开关，就是在启用断路器的情况下，不能继续存款了，但是可以取款

#### 减速器

减速器减缓了行动，因此，如果发生恶意行动，有时间恢复。例如，The DAO [blockchainsllc/DAO: The Standard DAO Framework, including Whitepaper (github.com)](https://github.com/blockchainsllc/DAO) 要求在成功请求分裂DAO和能够分裂之间有27天的时间。这确保了资金被保存在合约内，增加了恢复的可能性。在The DAO的案例中，在减速带给出的时间内没有可以采取的有效行动，但与我们的其他技术相结合，它们可以相当有效。

```d
struct RequestedWithdrawal {
    uint amount;
    uint time;
}

mapping (address => uint) private balances;
mapping (address => RequestedWithdrawal) private requestedWithdrawals;
uint constant withdrawalWaitPeriod = 28 days; // 4 weeks

function requestWithdrawal() public {
    if (balances[msg.sender] > 0) {
        uint amountToWithdraw = balances[msg.sender];
        balances[msg.sender] = 0; // for simplicity, we withdraw everything;
        // presumably, the deposit function prevents new deposits when withdrawals are in progress

        requestedWithdrawals[msg.sender] = RequestedWithdrawal({
            amount: amountToWithdraw,
            time: block.timestamp
        });
    }
}

function withdraw() public {
    if(requestedWithdrawals[msg.sender].amount > 0 && block.timestamp > requestedWithdrawals[msg.sender].time + withdrawalWaitPeriod) {
        uint amountToWithdraw = requestedWithdrawals[msg.sender].amount;
        requestedWithdrawals[msg.sender].amount = 0;

        require(msg.sender.send(amountToWithdraw));
    }
}

```

#### 速率限制

速率限制停止或需要批准重大变化。例如，存款人可能只被允许在某一时间段内提取一定数额或总存款的百分比（例如，在1天内最多提取100个乙醚）--在该时间段内的额外提取可能会失败或需要某种特别批准。或者速率限制可以是在合约层面，在一个时间段内，合约只发行一定数量的代币。

```d
uint internal period; // how many blocks before limit resets
uint internal limit; // max ether to withdraw per period
uint internal currentPeriodEnd; // block which the current period ends at
uint internal currentPeriodAmount; // amount already withdrawn this period

constructor(uint _period, uint _limit) public {
    period = _period;
    limit = _limit;

    currentPeriodEnd = block.number + period;
}

function withdraw(uint amount) public {
    // Update period before proceeding
    updatePeriod();

    // Prevent overflow
    uint totalAmount = currentPeriodAmount + amount;
    require(totalAmount >= currentPeriodAmount, 'overflow');

    // Disallow withdraws that exceed current rate limit
    require(currentPeriodAmount + amount < limit, 'exceeds period limit');
    currentPeriodAmount += amount;
    msg.sender.transfer(amount);
}

function updatePeriod() internal {
    if(currentPeriodEnd < block.number) {
        currentPeriodEnd = block.number + period;
        currentPeriodAmount = 0;
    }
}

```

#### 部署

合约应该有一个相当长的测试期--在大量资金被置于风险之中之前。

至少，你应该

- 有一个完整的测试套件，具有100%的测试覆盖率（或接近于此）。
- 在你自己的测试网上进行部署
- 在公共测试网上部署，并进行大量的测试和错误悬赏。
- 详尽的测试应该允许各种玩家与合约进行大量的互动。
- 在主网上部署测试版，对风险金额进行限制

> 然而，做到这点的合约项目方太少了，Github上充斥着大量缺少文档，缺少单元测试的合约项目，大量的合约项目互相抄袭借鉴，没有漏洞才见鬼了

在测试过程中，你可以通过阻止任何动作，在一定的时间段后，强制进行自动废止。例如，一个阿尔法合约可以工作几个星期，然后自动关闭所有的动作，除了最后的退出。

```d
modifier isActive() {
    require(block.number <= SOME_BLOCK_NUMBER);
    _;
}

function deposit() public isActive {
    // some code
}

function withdraw() public {
    // some code
}

```

在早期阶段，你可以限制任何用户（或整个合约）的以太币数量 - 减少风险。

#### 安全庇护所

[Safe Haven - Ethereum Smart Contract Best Practices (consensys.github.io)](https://consensys.github.io/smart-contract-best-practices/development-recommendations/precautions/safe-haven/) 

### solidity语言相关

#### Assert, Require， Revert

##### 用assert()强制执行不变式

当assert失败时，断言防护就会被触发--比如一个不变的属性改变。例如，在一个代币发行合约中，代币与以太币的发行比例可能是固定的。你可以用assert()来验证在任何时候这都是事实。断言防护通常应与其他技术相结合，例如暂停合约并允许升级。(否则，你可能会被卡住，因为断言总是失败的）。

例子：

```d
contract Token {
    mapping(address => uint) public balanceOf;
    uint public totalSupply;

    function deposit() public payable {
        balanceOf[msg.sender] += msg.value;
        totalSupply += msg.value;
        assert(address(this).balance >= totalSupply);
    }
}

```

请注意，断言并不是严格意义上的余额相等，因为合约可以不通过deposit()函数而强行发送以太币！

##### 把assert，require，revert用在合适的场景

> 函数assert和require可以用来方便地检查条件，如果不符合条件，就抛出一个异常。

assert函数应该只用于测试内部错误，以及检查不变量。

require函数应该被用来确保有效的条件 [Expressions and Control Structures — Solidity 0.8.18 documentation (soliditylang.org)](https://docs.soliditylang.org/en/latest/control-structures.html#panic-via-assert-and-error-via-require) ，如输入，或合约状态变量被满足，或验证调用外部合约的返回值。

遵循这一范式，形式化分析工具可以验证无效的操作码永远无法到达：这意味着代码中没有违反不变量，代码得到了形式化的验证。

```d
pragma solidity ^0.5.0;

contract Sharer {
    function sendHalf(address payable addr) public payable returns (uint balance) {
        require(msg.value % 2 == 0, "Even value required."); //Require() can have an optional message string
        uint balanceBeforeTransfer = address(this).balance;
        (bool success, ) = addr.call.value(msg.value / 2)("");
        require(success);
        // Since we reverted if the transfer failed, there should be
        // no way for us to still have half of the money.
        assert(address(this).balance == balanceBeforeTransfer - msg.value / 2); // used for internal error checking
        return address(this).balance;
    }
}

```

[SWC-110 · Overview (swcregistry.io)](https://swcregistry.io/docs/SWC-110)
[SWC-123 · Overview (swcregistry.io)](https://swcregistry.io/docs/SWC-123)

> 我的理解就是，require用在检查合约的方法传入的外部参数，让参数满足条件，而assert是用在合约内部自身状态的错误，用断言来保证合约自身状态的合理

#### 用Modifier来保护

修改器内的代码通常在函数主体之前执行，所以任何状态变化或外部调用都会违反 "检查-影响-互动 "模式 https://solidity.readthedocs.io/en/develop/security-considerations.html#use-the-checks-effects-interactions-pattern 。此外，这些语句也可能不被开发者注意到，因为修改器的代码可能远离函数声明。例如，修改器中的外部调用会导致重入攻击。

```d
contract Registry {
    address owner;

    function isVoter(address _addr) external returns(bool) {
        // Code
    }
}

contract Election {
    Registry registry;

    modifier isEligible(address _addr) {
        require(registry.isVoter(_addr));
        _;
    }

    function vote() isEligible(msg.sender) public {
        // Code
    }
}

```

在这种情况下，Registry合约可以通过在`isVoter()`内调用`Election.vote()`来进行重入攻击。

> 使用修改器 https://solidity.readthedocs.io/en/develop/contracts.html#function-modifiers 来替换多个函数中重复的条件检查，如isOwner()，否则在函数内部使用require或revert。这使你的智能合约代码更具可读性，更容易审计。

#### 整数除法

所有的整数除法都会向下舍入到最接近的整数。如果你需要更多的精度，考虑使用一个乘法器，或者同时存储分子和分母。

(在未来，Solidity将有一个fixed-point类型 [Types — Solidity 0.8.18 documentation (soliditylang.org)](https://docs.soliditylang.org/en/develop/types.html#fixed-point-numbers) ，这将使其更容易。)

```d
// bad
uint x = 5 / 2; // Result is 2, all integer division rounds DOWN to the nearest integer
```

使用乘数可以防止四舍五入，这个乘数需要在将来处理x的时候加以考虑。

```d
// good
uint multiplier = 10;
uint x = (5 * multiplier) / 2;
```

储存分子和分母意味着你可以在链外计算分子/分母的结果。

```d
// good
uint numerator = 5;
uint denominator = 2;
```

#### Abstrate vs Interfaces

要意识到抽象合约和接口之间的权衡。

接口和抽象合约都为智能合约提供了一个可定制和可重用的方法。在Solidity 0.4.11中引入的接口，与抽象合约类似，但不能实现任何功能。接口也有一些限制，如不能访问存储或继承其他接口，这通常使抽象合约更实用。虽然，接口对于在实现前设计合约来说肯定是有用的。此外，重要的是要记住，如果一个合约继承自一个抽象合约，它必须通过重写实现所有非实现的功能，否则它也将是抽象的。

#### Fallback函数

##### 保持fallback函数的简洁

当合约被发送一个没有参数的消息时（或没有函数匹配时），回退函数会被调用，当从`.send()`或`.transfer()`调用时，只能访问2300个gas。如果你希望能够从`.send()`或`.transfer()`中接收以太币，你在fallback函数中最多只能记录一个事件。如果需要进行更多gas的计算，请使用一个合适的函数。

```d
// bad
function() payable { balances[msg.sender] += msg.value; }

// good
function deposit() payable external { balances[msg.sender] += msg.value; }

function() payable { require(msg.data.length == 0); emit LogDepositReceived(msg.sender); }
```

##### 在fallback函数中检查数据长度

由于fallback函数不仅在普通的以太币传输（没有数据）时被调用，而且在没有其他函数匹配时也会被调用，如果回退函数只打算用于记录收到的以太币，那么你应该检查数据是否为空。否则，如果你的合约使用不当，调用不存在的函数，调用者将不会注意到。

```d
// bad
function() payable { emit LogDepositReceived(msg.sender); }

// good
function() payable { require(msg.data.length == 0); emit LogDepositReceived(msg.sender); }
```

#### 可支付性

从Solidity 0.4.0开始，每个接收以太币的函数都必须使用payable修改器，否则如果交易的msg.value>0将被revert（除非是强制 [Force Feeding - Ethereum Smart Contract Best Practices (consensys.github.io)](https://consensys.github.io/smart-contract-best-practices/attacks/force-feeding/) ）。

> 一些可能不明显的东西：paybale修改器只适用于来自外部合约的调用。如果我在同一合约中的payable函数中调用一个非payable函数，非payable函数不会失败，尽管msg.value仍然被设置。

#### 可见性

明确标注函数和状态变量的可见性。函数可以被指定为`external`、`public`、`internal`或`private`。请理解它们之间的区别，例如，`external`可能足以代替`public`。对于状态变量，`external`是不可能的。明确标注可见性将更容易抓住关于谁可以调用函数或访问变量的不正确假设。

- `external`函数是合约接口的一部分。一个`external`函数`foo`不能被内部调用（即`foo()`不能工作，但`this.foo()`可以工作）。当`external`函数接收大的数据数组时，有时会更有效率。
- `public`函数是合约接口的一部分，既可以在内部调用也可以通过消息调用。对于public状态变量，会自动生成一个getter函数（见下文）。
- `internal`函数和状态变量只能在内部访问，不使用`this`。
- `private`函数和状态变量只对它们所定义的合约可见，在派生合约中不可见。注意：区块链外部的所有观察者都可以看到合约内的一切，甚至是私有变量。[Contracts — Solidity 0.8.18 documentation (soliditylang.org)](https://docs.soliditylang.org/en/develop/contracts.html#visibility-and-getters) 

```d
// bad
uint x; // the default is internal for state variables, but it should be made explicit
function buy() { // the default is public
    // public code
}

// good
uint private y;
function buy() external {
    // only callable externally or using this.buy()
}

function utility() public {
    // callable externally, as well as internally: changing this code requires thinking about both cases.
}

function internalAction() internal {
    // internal code
}
```

[SWC-100 · Overview (swcregistry.io)](https://swcregistry.io/docs/SWC-100)

[SWC-108 · Overview (swcregistry.io)](https://swcregistry.io/docs/SWC-108) 

#### 锁住 pragmas

合约应该用它们经过最多测试的编译器版本和标志来部署。锁定pragma有助于确保合约不会意外地使用最新的编译器进行部署，例如，最新的编译器可能有更高的未发现的错误风险。合约也可能被其他人部署，而`pragma`表明原作者打算使用的编译器版本。

```d
// bad
pragma solidity ^0.4.4;


// good
pragma solidity 0.4.4;
```

注意：一个浮动的pragma版本（即`^0.4.25`）将与`0.4.26-nightly.2018.9.25`编译得很好，然而nightly build绝不应该用来编译生产用代码。

> 警告： 当一个合约打算被其他开发者消费时，Pragma语句可以被允许浮动，比如library或EthPM包中的合约。否则，开发者将需要手动更新pragma，以便在本地编译。[SWC-103 · Overview (swcregistry.io)](https://swcregistry.io/docs/SWC-103) 

#### 事件监控

在合约部署后，有一种方法可以监测合约的活动，这很有用。实现这一目标的方法之一是查看合约的所有交易，然而这可能是不够的，因为合约之间的消息调用没有记录在区块链上。此外，它只显示输入参数，而不是正在对状态进行的实际改变。另外，事件可以被用来触发用户界面中的功能。

```d
contract Charity {
    mapping(address => uint) balances;

    function donate() payable public {
        balances[msg.sender] += msg.value;
    }
}

contract Game {
    function buyCoins() payable public {
        // 5% goes to charity
        charity.donate.value(msg.value / 20)();
    }
}
```

在这里，`Game`合约将对`Charity.donate()`进行一次内部调用。这个交易不会出现在`Charity`的外部交易列表中，而只在内部交易中可见。

事件是记录合约中发生的事情的一种便捷方式。发出的事件与其他合约数据一起留在区块链中，它们可供未来审计。下面是对上述例子的改进，使用事件来提供慈善机构的捐赠历史。

```d
contract Charity {
    // define event
    event LogDonate(uint _amount);

    mapping(address => uint) balances;

    function donate() payable public {
        balances[msg.sender] += msg.value;
        // emit event
        emit LogDonate(msg.value);
    }
}

contract Game {
    function buyCoins() payable public {
        // 5% goes to charity
        charity.donate.value(msg.value / 20)();
    }
}
```

在这里，所有通过慈善组织合约的交易，不管是直接的还是不直接的，都会在该合约的事件列表中显示出来，同时还有捐赠的金额。

> 倾向于使用 `selfdestruct`（而不是`suicide`）和 `keccak256`（而不是 `sha3`）等结构/别名。类似 `require(msg.sender.send(1 ether))` 的模式也可以简化为使用 `transfer()` ，如 `msg.sender.transfer(1 ether)`。请查看 Solidity changelog以了解更多类似的变化。

#### 遮挡(shadowing)

目前，在 Solidity 中可以对内置全局变量进行遮挡 https://en.wikipedia.org/wiki/Variable_shadowing 。这允许合约覆盖诸如 `msg` 和 `revert()` 等内置全局的功能。尽管这是有意为之，但它可能会误导合约的用户，阻止其了解合约的真实行为。[Warn if built-in globals are shadowed · Issue #1249 · ethereum/solidity (github.com)](https://github.com/ethereum/solidity/issues/1249) 

```d
contract PretendingToRevert {
    function revert() internal constant {}
}

contract ExampleContract is PretendingToRevert {
    function somethingBad() public {
        revert();
    }
}
```

合约用户（和审计师）应该了解他们打算使用的任何应用程序的完整智能合约源代码。

#### tx.origin

永远不要使用`tx.origin`进行授权，另一个合约可以有一个方法来调用你的合约（例如用户有一些资金），你的合约将授权该交易，因为你的地址在`tx.origin`中。

```d
contract MyContract {

    address owner;

    function MyContract() public {
        owner = msg.sender;
    }

    function sendTo(address receiver, uint amount) public {
        require(tx.origin == owner);
        (bool success, ) = receiver.call.value(amount)("");
        require(success);
    }

}

contract AttackingContract {

    MyContract myContract;
    address attacker;

    function AttackingContract(address myContractAddress) public {
        myContract = MyContract(myContractAddress);
        attacker = msg.sender;
    }

    function() public {
        myContract.sendTo(attacker, msg.sender.balance);
    }

}
```

您应该使用`msg.sender`进行授权（如果另一个合约调用您的合约`msg.sender`将是合约的地址而不是调用合约的用户的地址）。

> 除了授权的问题，tx.origin有可能在未来从以太坊协议中删除，所以使用tx.origin的代码将不兼容未来的版本 Vitalik："不要假设tx.origin将继续可用或有意义。[contract design - How do I make my DAPP "Serenity-Proof?" - Ethereum Stack Exchange](https://ethereum.stackexchange.com/questions/196/how-do-i-make-my-dapp-serenity-proof/200#200) 

> 还值得一提的是，通过使用`tx.origin`，你限制了合约之间的互操作性，因为使用`tx.origin`的合约不能被其他合约使用，因为合约不能成为`tx.origin`。

[SWC-115 · Overview (swcregistry.io)](https://swcregistry.io/docs/SWC-115)

#### 时间戳依赖

当使用时间戳来执行合约中的关键功能时，有三个主要考虑因素，特别是当行动涉及资金转移时。

##### 时间戳操纵

请注意，区块的时间戳可以被矿工操纵。考虑一下这个合约。[theRun | Address 0xcac337492149bdb66b088bf5914bedfbf78ccc18 | Etherscan](https://etherscan.io/address/0xcac337492149bdb66b088bf5914bedfbf78ccc18#code) 

```d
uint256 constant private salt =  block.timestamp;

function random(uint Max) constant private returns (uint256 result){
    //get the best seed for randomness
    uint256 x = salt * 100/Max;
    uint256 y = salt * block.number/(salt % 5) ;
    uint256 seed = block.number/3 + (salt % 300) + Last_Payout + y;
    uint256 h = uint256(block.blockhash(seed));

    return uint256((h / x)) % Max + 1; //random number between 1 and Max
}
```

当合约使用时间戳作为随机数的种子时，矿工实际上可以在区块被验证的15秒内发布一个时间戳，有效地让矿工预先计算出一个更有利于他们在抽奖中的机会的选项。时间戳不是随机的，不应该在这种情况下使用。

##### 15秒规则

黄皮书 https://ethereum.github.io/yellowpaper/paper.pdf （以太坊的参考规范）没有规定区块的时间漂移程度的约束，但它确实规定每个时间戳应该大于其父的时间戳 [How do Ethereum mining nodes maintain a time consistent with the network? - Ethereum Stack Exchange](https://ethereum.stackexchange.com/questions/5924/how-do-ethereum-mining-nodes-maintain-a-time-consistent-with-the-network/5926#5926) 。流行的Ethereum协议实现Geth [go-ethereum/consensus.go at 4e474c74dc2ac1d26b339c32064d0bac98775e77 · ethereum/go-ethereum (github.com)](https://github.com/ethereum/go-ethereum/blob/4e474c74dc2ac1d26b339c32064d0bac98775e77/consensus/ethash/consensus.go#L45) 和Parity [parity-ethereum/verification.rs at 73db5dda8c0109bb6bc1392624875078f973be14 · openethereum/parity-ethereum (github.com)](https://github.com/openethereum/parity-ethereum/blob/73db5dda8c0109bb6bc1392624875078f973be14/ethcore/src/verification/verification.rs#L296-L307) 都拒绝时间戳超过15秒的区块的未来。因此，在评估时间戳的使用时，一个好的经验法则是。

> 如果你的时间相关事件的规模可以变化15秒并保持完整性，使用block.timestamp是安全的。

##### 避免使用`block.number` 作为时间戳

使用`block.number`属性和平均block时间 [Ethereum Average Block Time Chart | Etherscan](https://etherscan.io/chart/blocktime) 来估计时间差是可能的，但是这并不能证明未来，因为block时间可能会发生变化（如分叉重组 [Chain Reorganisation Depth Expectations | Ethereum Foundation Blog](https://blog.ethereum.org/2015/08/08/chain-reorganisation-depth-expectations) 和难度炸弹 [Metropolis Difficulty Bomb Delay · Issue #649 · ethereum/EIPs (github.com)](https://github.com/ethereum/EIPs/issues/649) ）。在跨越几天的销售中，15秒规则允许人们实现更可靠的时间估计。

[SWC-116 · Overview (swcregistry.io)](https://swcregistry.io/docs/SWC-116) 

#### 复杂继承

当在Solidity中利用多重继承时，理解编译器如何组成继承图是很重要的。

```d
contract Final {
    uint public a;
    function Final(uint f) public {
        a = f;
    }
}

contract B is Final {
    int public fee;

    function B(uint f) Final(f) public {
    }
    function setFee() public {
        fee = 3;
    }
}

contract C is Final {
    int public fee;

    function C(uint f) Final(f) public {
    }
    function setFee() public {
        fee = 5;
    }
}

contract A is B, C {
  function A() public B(3) C(5) {
      setFee();
  }
}
```

当一个合约被部署时，编译器将从右到左线性化继承（在关键字之后，父类被从最基础的到最派生的列出）。下面是合约A的线性化。

**Final \<- B \<- C \<- A**

线性化的后果将产生一个费用值5，因为C是最派生的合约。这似乎是显而易见的，但想象一下这样的场景：C能够遮挡 [[#遮挡(shadowing)]] 关键的函数，重新排序boolean条款，并导致开发人员编写可利用(exploiteble)的合约。静态分析目前没有提出遮挡函数的问题，所以必须手动检查。

[Phil Does Security | Solidity anti-patterns: Fun with inheritance DAG abuse (pdaian.com)](https://pdaian.com/blog/solidity-anti-patterns-fun-with-inheritance-dag-abuse/) 

[Consolidate inheritance rules (github.com)](https://github.com/ethereum/solidity/projects/9#card-8027020)

[SWC-125 · Overview (swcregistry.io)](https://swcregistry.io/docs/SWC-125) 

#### 接口类型

当一个函数需要一个合约地址作为参数时，最好是传递一个接口或合约类型，而不是一个原始地址。如果该函数在源代码的其他地方被调用，编译器将提供额外的类型安全保证。

在这里，我们看到两种选择。

```d
contract Validator {
    function validate(uint) external returns(bool);
}

contract TypeSafeAuction {
    // good
    function validateBet(Validator _validator, uint _value) internal returns(bool) {
        bool valid = _validator.validate(_value);
        return valid;
    }
}

contract TypeUnsafeAuction {
    // bad
    function validateBet(address _addr, uint _value) internal returns(bool) {
        Validator validator = Validator(_addr);
        bool valid = validator.validate(_value);
        return valid;
    }
}
```

那么从下面的例子中可以看出使用上述`TypeSafeAuction`合约的好处。如果在调用`validateBet()`时使用了一个地址参数，或者使用了`Validator`以外的合约类型，编译器会抛出这个错误。

```d
contract NonValidator{}

contract Auction is TypeSafeAuction {
    NonValidator nonValidator;

    function bet(uint _value) {
        bool valid = validateBet(nonValidator, _value); // TypeError: Invalid type for argument in function call.
                                                        // Invalid implicit conversion from contract NonValidator
                                                        // to contract Validator requested.
    }
}
```

####  EXTCODESIZE 检查

避免使用`extcodesize`来检查外部拥有的账户。

下面的modifier（或类似的检查）经常被用来验证一个调用是由外部拥有的账户（EOA）还是合约账户发出的。

```d
// bad
modifier isNotContract(address _a) {
  uint size;
  assembly {
    size := extcodesize(_a)
  }
    require(size == 0);
     _;
}
```

这个想法很直接：如果一个地址包含代码，它就不是一个EOA，而是一个合约账户。然而，一个合约在构建过程中并没有源代码可用。这意味着当构造器运行时，它可以对其他合约进行调用，但其地址的`extcodesize`返回0。下面是一个最小的例子，说明如何规避这个检查。

```d
contract OnlyForEOA {    
    uint public flag;

    // bad
    modifier isNotContract(address _a){
        uint len;
        assembly { len := extcodesize(_a) }
        require(len == 0);
        _;
    }

    function setFlag(uint i) public isNotContract(msg.sender){
        flag = i;
    }
}

contract FakeEOA {
    constructor(address _a) public {
        OnlyForEOA c = OnlyForEOA(_a);
        c.setFlag(1);
    }
}
```

因为合约地址可以预先计算，如果它检查的地址在块n时是空的，但在大于n的某个块时有一个合约部署到它，那么这个检查也可能失败。

> 如果你的目标是防止其他合约能够调用你的合约，那么extcodesize检查可能就足够了。另一种方法是检查（tx.origin == msg.sender）的值，尽管这也有缺点。

> 可能还有其他情况下，extcodesize检查可以满足您的目的。在这里描述所有这些情况是超出范围的。理解EVM的基本行为并使用你的判断。

### Token代币相关

#### 标准化

一般来说，代币的智能合约应该遵循一个公认的、稳定的标准。

目前接受的标准的例子包括。

- EIP20
- EIP721（不可伪造的代币）
- 更多信息请见eips.ethereum.org

#### Frontrunning

EIP-20 token的`approve()`函数为被批准的spender创造了超过预期金额的可能性。可以使用frontrunning攻击，使被批准的spender在调用`approve()`处理之前和之后都能调用`transferFrom()`。更多细节可在EIP [EIPs/eip-20.md at master · ethereum/EIPs (github.com)](https://github.com/ethereum/EIPs/blob/master/EIPS/eip-20.md#approve) 和本文件 [ERC20 API: An Attack Vector on Approve/TransferFrom Methods - Google Docs](https://docs.google.com/document/d/1YLPtQxZu1UAvO9cZ1O2RPXBbT0mooh4DYKjA_jp-RLM/edit) 中找到。 

#### 零地址

在撰写本报告时，"零 "地址0x0000000000000000000000000000000000000000 https://etherscan.io/address/0x0000000000000000000000000000000000000000 持有的代币价值超过了8000万美元。

> 这里是提示大家转账别转错，转错了就没救了，一般这个地址被当做销毁代币的地址，所以有那么多钱

#### 合约地址

还要考虑防止代币转移到智能合约的同一地址。

EOS代币智能合约 [EOS: Old Token | Address 0x86fa049857e0209aa7d9e616f7eb3b3b78ecfdb0 | Etherscan](https://etherscan.io/address/0x86fa049857e0209aa7d9e616f7eb3b3b78ecfdb0) 就是一个例子，超过90,000个代币被卡在合约地址上，这说明留下这个漏洞可能造成的损失。

例子:

实施上述两个建议的一个例子是创建以下修改器；验证 "to "的地址既不是0x0也不是智能合约自己的地址。

```d
    modifier validDestination( address to ) {
        require(to != address(0x0));
        require(to != address(this) );
        _;
    }

```

然后，该修改器应被应用于 "transfer "和 "transferFrom "方法。

```d
    function transfer(address _to, uint _value)
        validDestination(_to)
        returns (bool) 
    {
        (... your logic ...)
    }

    function transferFrom(address _from, address _to, uint _value)
        validDestination(_to)
        returns (bool) 
    {
        (... your logic ...)
    }

```

### 文档

#### 常规

当启动一个将有大量资金或被要求是关键任务的合约时，列入适当的文件是很重要的。

#### 规范

- 规格书、图表、状态机、模型和其他帮助审核员、审查员和社区了解系统意图的文件。
- 许多bug可以从规范中找到，而且它们的修复成本最低。
- 推广计划，包括这里列出的细节  [[#部署]]  以及目标日期。

#### 状态

- 当前代码的部署地点
- 编译器版本、使用的标志以及验证部署的字节码与源代码匹配的步骤
- 将用于推广的不同阶段的编译器版本和标志。
- 已部署代码的当前状态（包括未解决的问题、性能统计等）。

#### 过程

- 发现错误时的行动计划（例如，应急方案、公共通知程序等）。
- 如果出了问题的清算过程（例如，资助者将在攻击前从剩余资金中获得你的余额的百分比）
- 负责任的披露政策（例如，在哪里报告发现的错误，任何错误赏金计划的规则）。
- 失败时的追索权（例如，保险、惩罚基金、无追索权）。

#### 已知问题

- 合约的主要风险
- 例如，你可以失去所有的钱，黑客可以投票给某些结果。
- 所有已知的错误/限制
- 潜在的攻击和缓解措施
- 潜在的利益冲突（例如，将利用你自己，就像Slock.it对DAO所做的）。

#### 历史

- 测试（包括使用情况统计，发现的错误，测试的长度）
- 审查过代码的人（以及他们的主要反馈）。

#### 联系方式

- 有问题应与谁联系
- 程序员和/或其他重要方面的名字
- 可以提出问题的聊天室

## 攻击

[Index - Ethereum Smart Contract Best Practices (consensys.github.io)](https://consensys.github.io/smart-contract-best-practices/attacks/)

## 安全工具

[Index - Ethereum Smart Contract Best Practices (consensys.github.io)](https://consensys.github.io/smart-contract-best-practices/security-tools/) 



