
[Trading Strategies & Indicators Built by TradingView Community](https://www.tradingview.com/script/jc2hs2qK-Price-and-Volume-Breakout-Buy-Strategy-TradeDots/)

![[Pasted image 20240621173101.png]]

“价格和成交量突破买入策略”是一种交易策略，旨在通过检测指定烛台上的同一时刻的价格和成交量突破来识别买入机会。  
  
该策略针对表现出高波动性和显着动量峰值的资产进行了优化。  
  
**HOW IT WORKS**  
  
该策略首先将特定数量的烛台作为价格和交易量的检查窗口。  
  
这些值用作识别突破条件的基准。  
  
当收盘价和交易量都超过预定窗口内观察到的最大值时，开始交易。  
  
价格必须高于指定的移动平均线(MA)，作为趋势指标，确保所有交易都与当前市场趋势保持一致。  
  
**APPLICATION**  
  
这种策略对于比特币和以太坊等高度波动的资产特别有效，它利用了价格和交易量突然突破的线索，表明市场出现重动，通常由市场聪明的货币交易者推动。  
  
然而，对于像标准普尔 500 指数这样的更广泛市场，由于与加密货币市场相比，交易量和价格变化不那么明显，这种策略可能不太有效。  
  
**DEFAULT SETUP**  
  
佣金：0.01%  
初始资本：10,000  
美元 每笔交易的权益：70%  
  
回测结果有时会在某些更高的时间范围内给出少于 100 笔交易，因为大多数交易往往持有期很长。进入条件也更加严格，再加上加密货币相对短暂的历史，导致在更长的时间范围内交易较少。  
  
建议用户调整和个性化此交易策略，以更好地匹配他们的个人交易偏好和风格。  
  
**RISK DISCLAIMER**  
  
交易会带来巨大的风险，大多数日内交易者都会蒙受损失。TradeDots 提供的所有内容、工具、脚本、文章和教育纯粹用于信息和教育目的。过去的表现并不是未来结果的明确预测指标。


**Script**

![[Pasted image 20240621172605.png]]

Pine Script

```pascal
// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © tradedots

//@version=5
strategy("Price and Volume Breakout Buy Strategy [TradeDots]", overlay=true, initial_capital = 10000, default_qty_type = strategy.percent_of_equity, default_qty_value = 70, commission_type = strategy.commission.percent, commission_value = 0.01)

input_price_breakout_period = input.int(60, "Price Breakout Period")
input_volume_breakout_period = input.int(60, "Volume Breakout Period")
input_trendline_legnth = input.int(200, "Trendline Length")
input_order_direction = input.string("Long", options = ["Long", "Short", "Long and Short"], title = "Order Direction")

price_highest = ta.highest(input_price_breakout_period)
price_lowest = ta.lowest(input_price_breakout_period)
volume_highest = ta.highest(volume, input_volume_breakout_period)

// Long Orders
if close > price_highest[1] and volume > volume_highest[1] and close > ta.sma(close, input_trendline_legnth) and strategy.opentrades == 0 and input_order_direction != "Short"
    strategy.entry("Long", strategy.long)
    line.new(bar_index[input_price_breakout_period], price_highest[1], bar_index, price_highest[1], color = #9cff87, width = 2)
    label.new(bar_index,low, "🟢 Breakout Buy", style = label.style_label_up, color = #9cff87)

// Close when price is below moving average for 5 consecutive days
if close < ta.sma(close, input_trendline_legnth) and close[1] < ta.sma(close, input_trendline_legnth) and close[2] < ta.sma(close, input_trendline_legnth) and close[3] < ta.sma(close, input_trendline_legnth) and close[4] < ta.sma(close, input_trendline_legnth) and strategy.opentrades.size(strategy.opentrades - 1) > 0
    strategy.close("Long")
    label.new(bar_index, high, "🔴 Close Position", style = label.style_label_down, color = #f9396a, textcolor = color.white)

// Short Orders
if close < price_lowest[1] and volume > volume_highest[1] and close < ta.sma(close, input_trendline_legnth) and strategy.opentrades == 0 and input_order_direction != "Long"
    strategy.entry("Short", strategy.short)
    line.new(bar_index[input_price_breakout_period], price_lowest[1], bar_index, price_lowest[1], color = #f9396a, width = 2)
    label.new(bar_index,high , "🔴 Breakout Sell", style = label.style_label_down, color = #f9396a, textcolor = color.white)

// Close when price is above moving average for 5 consecutive days
if close > ta.sma(close, input_trendline_legnth) and close[1] > ta.sma(close, input_trendline_legnth) and close[2] > ta.sma(close, input_trendline_legnth) and close[3] > ta.sma(close, input_trendline_legnth) and close[4] > ta.sma(close, input_trendline_legnth) and strategy.opentrades.size(strategy.opentrades - 1) < 0
    strategy.close("Short")
    label.new(bar_index, low, "🟢 Close Position", style = label.style_label_up, color = #9cff87)

plot(ta.sma(close, input_trendline_legnth), color = color.white, linewidth = 2)
plotcandle(open, high, low, close, title='Candles', color = (close > ta.sma(close, input_trendline_legnth) ? #9cff87 : #f9396a), wickcolor=(close > ta.sma(close, input_trendline_legnth) ? #9cff87 : #f9396a), force_overlay = true)


```

## 评论

> 加入百分比止损和sma止盈可以使策略更有效，但是这个策略在低波动性场景下基本没什么作用。