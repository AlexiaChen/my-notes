
[Trading Strategies & Indicators Built by TradingView Community](https://www.tradingview.com/script/M4y5HT3h-Triple-EMA-QQE-Trend-Following-Strategy-TradeDots/)

![[Pasted image 20240621173141.png]]


â€œä¸‰é‡ EMA + QQE è¶‹åŠ¿è·Ÿè¸ªç­–ç•¥â€åˆ©ç”¨ä¸¤ä¸ªå¤æ‚çš„æŠ€æœ¯æŒ‡æ ‡çš„åŠ›é‡ï¼Œå³ä¸‰é‡æŒ‡æ•°ç§»åŠ¨å¹³å‡çº¿ ï¼ˆTEMAï¼‰ å’Œå®šæ€§å®šé‡ä¼°è®¡ ï¼ˆQQEï¼‰ï¼Œä»¥ç”Ÿæˆç²¾ç¡®çš„ä¹°å…¥å’Œå–å‡ºä¿¡å·ã€‚è¯¥ç­–ç•¥æ“…é•¿é€šè¿‡è¯†åˆ«çŸ­æœŸä»·æ ¼åŠ¨é‡å’ŒåŠ¨æ€è¶…ä¹°æˆ–è¶…å–æ¡ä»¶æ¥æ•æ‰è¶‹åŠ¿å˜åŒ–ã€‚


## How it works

è¯¥ç­–ç•¥é›†æˆäº†ä¸¤ä¸ªå…³é”®æŒ‡æ ‡ï¼š  
  
**ä¸‰é‡æŒ‡æ•°ç§»åŠ¨å¹³å‡çº¿ ï¼ˆTEMAï¼‰ï¼š**TEMA é€šè¿‡å‡å°‘æ»žåŽå’Œæ›´æœ‰æ•ˆåœ°å¹³æ»‘æ•°æ®æ¥å¢žå¼ºä¼ ç»Ÿç§»åŠ¨å¹³å‡çº¿ã€‚å®ƒé€šè¿‡å°† EMA å…¬å¼åº”ç”¨äºŽä»·æ ¼ä¸‰æ¬¡æ¥å®žçŽ°è¿™ä¸€ç‚¹ï¼Œå¦‚ä¸‹æ‰€ç¤ºï¼š

```pascal
// pine script
tema(src,Â length)Â =>  
Â Â Â Â ema1Â =Â ta.ema(src,Â length)  
Â Â Â Â ema2Â =Â ta.ema(ema1,Â length)  
Â Â Â Â ema3Â =Â ta.ema(ema2,Â length)  
Â Â Â Â temaÂ =Â 3*ema1Â -Â 3*ema2Â +Â ema3
```

è¿™ç§è®¡ç®—æœ‰åŠ©äºŽæé«˜å¯¹ä»·æ ¼å˜åŠ¨çš„æ•æ„Ÿæ€§ã€‚  
  
**å®šæ€§å®šé‡ä¼°è®¡ ï¼ˆQQEï¼‰ï¼š**QQE æŒ‡æ ‡é€šè¿‡çº³å…¥å¹³æ»‘æœºåˆ¶æ¥æ”¹è¿›æ ‡å‡† RSIã€‚å®ƒä»Žæ ‡å‡† RSI å¼€å§‹ï¼Œåœ¨æ­¤ RSI ä¸Šå åŠ  5 å‘¨æœŸ EMAï¼Œç„¶åŽä½¿ç”¨ 27 å‘¨æœŸ EMA çš„åŒé‡åº”ç”¨æ¥å¢žå¼ºç»“æžœã€‚ç„¶åŽé€šè¿‡å°†ç»“æžœä¹˜ä»¥å› å­æ•°æ¥å¾—å‡ºæ…¢é€Ÿå°¾éšçº¿ã€‚è¿™ç§æ–¹æ³•å»ºç«‹äº†ä¸€ä¸ªæ›´ç²¾ç»†ã€æ›´ä¸ç´§å¼ çš„è¶‹åŠ¿è·Ÿè¸ªä¿¡å·ï¼Œè¡¥å……äº† TEMAï¼Œä»¥å¢žå¼ºæ³¢åŠ¨æ¡ä»¶ä¸‹çš„æ•´ä½“å¸‚åœºæ—¶æœºã€‚


## Application

å‚è€ƒå¯¹â€œäº¤æ˜“ç­–ç•¥â€çš„è§è§£ï¼Œç­–ç•¥å®žæ–½å¦‚ä¸‹ï¼š  
  
é¦–å…ˆï¼Œæˆ‘ä»¬ä½¿ç”¨ä¸¤æ¡TEMAçº¿ï¼šä¸€æ¡è®¾ç½®ä¸º20å‘¨æœŸï¼Œå¦ä¸€æ¡è®¾ç½®ä¸º40å‘¨æœŸã€‚ç„¶åŽæŒ‰ç…§ä»¥ä¸‹è§„åˆ™æ“ä½œï¼š  
  

1. 40å‘¨æœŸçš„TEMAæ­£åœ¨ä¸Šå‡  
    
2. 20 å‘¨æœŸ TEMA é«˜äºŽ 40 å‘¨æœŸ TEMA  
    
3. ä»·æ ¼æ”¶äºŽ20å‘¨æœŸTEMAä¸Šæ–¹  
    
4. ä»Šå¤©ä¸æ˜¯æ˜ŸæœŸä¸€  
    
5. RSI MA è¶Šè¿‡æ…¢é€Ÿè¿½è¸ªçº¿  
    

  
è¯¥ç­–ç•¥ä¸é‡‡ç”¨ä¸»åŠ¨æ­¢ç›ˆæœºåˆ¶;ç›¸åï¼Œå®ƒåˆ©ç”¨è¿½è¸ªæ­¢æŸè®©ä»·æ ¼è‡ªç„¶è¾¾åˆ°æ­¢æŸï¼Œä»Žè€Œæœ€å¤§é™åº¦åœ°æé«˜æ½œåœ¨åˆ©æ¶¦çŽ‡ã€‚

## Default setup

ä½£é‡‘ï¼š0.01%  
åˆå§‹èµ„æœ¬ï¼š$10,000  
æ¯ç¬”äº¤æ˜“å‡€å€¼ï¼š80%  
  
å»ºè®®ç”¨æˆ·è°ƒæ•´å’Œä¸ªæ€§åŒ–æ­¤äº¤æ˜“ç­–ç•¥ï¼Œä»¥æ›´å¥½åœ°åŒ¹é…ä»–ä»¬çš„ä¸ªäººäº¤æ˜“åå¥½å’Œé£Žæ ¼ã€‚

## Script

![[Pasted image 20240621172645.png]]

```pascal
// This Pine Scriptâ„¢ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© tradedots

//@version=5
strategy("Triple EMA + QQE Trend Following Strategy [TradeDots]", overlay = true, max_lines_count = 500, max_boxes_count = 100, initial_capital = 10000, currency = currency.USD, commission_type = strategy.commission.percent, commission_value = 0.01, default_qty_type = strategy.percent_of_equity, default_qty_value = 80)

//pipsize
GetPipSize() =>
    syminfo.mintick * (syminfo.type == "forex" ? 10 : 1)

pipSize = GetPipSize()

var ts = 0.
var float stopLoss = na

//qqe values
rsi_period = input(14, title='RSI Length', group = "QQE")
rsi_smoothing_period = input(5, title='RSI Smoothing Period', group = "QQE")
factor = input(4.238, title='QQE Factor', group = "QQE")

//tema
ma_source = input(close, "TEMA Source", inline="MA1", group="TEMA")
tema1_length = input.int(20, "TEMA #1 Length", minval=1,group="TEMA")
tema2_length = input.int(40, "TEMA #2 Length", minval=1,group="TEMA")

input_stop_loss = input.int(120, "Stop Loss (pip)", group = "Strategy")

tema(src, length) =>
    ema1 = ta.ema(src, length)
    ema2 = ta.ema(ema1, length)
    ema3 = ta.ema(ema2, length)

    tema = 3*ema1 - 3*ema2 + ema3

tema1 = tema(ma_source, tema1_length)
tema2 = tema(ma_source, tema2_length)

tema1Plot = plot(tema1, title = " TEMA #1", color = #c7f9cc, linewidth = 2)
tema2Plot = plot(tema2, title = " TEMA #2", color = #57cc99, linewidth = 2)

// calculating qqe
rsi = ta.rsi(close, rsi_period)
rsiMa = ta.ema(rsi, rsi_smoothing_period)
atrRsi = math.abs(rsiMa[1] - rsiMa)

final_smooth = rsi_period * 2 - 1
MaAtrRsi = ta.ema(atrRsi, final_smooth)
dar = ta.ema(MaAtrRsi, final_smooth) * factor

crossover = ta.crossover(rsiMa, ts)
crossunder = ta.crossunder(rsiMa, ts)

ts := nz(crossover ? rsiMa - MaAtrRsi * factor
  : crossunder ? rsiMa + MaAtrRsi * factor
  : rsiMa > ts ? math.max(rsiMa - MaAtrRsi * factor, ts)
  : math.min(rsiMa + MaAtrRsi * factor, ts), rsiMa)

// entry and exit conditions
long_condition = close > tema1 and tema1 > tema2 and tema2 > tema2[1] and ta.crossover(rsiMa, ts) and dayofweek != dayofweek.monday
short_condition = close < tema1 and tema1 < tema2 and tema2 < tema2[1] and ta.crossunder(rsiMa, ts) and dayofweek != dayofweek.monday

if long_condition and strategy.opentrades == 0
    strategy.entry("Long", strategy.long)
    label.new(bar_index, low, "ðŸŸ¢ Long", color = #9cff87, style = label.style_label_up)
    stopLoss := close - input_stop_loss * pipSize

else if short_condition and strategy.opentrades == 0
    strategy.entry("Short", strategy.short)
    label.new(bar_index, high, "ðŸ”´ Short", color = #f9396a, style = label.style_label_down, textcolor = color.white)
    stopLoss := close + input_stop_loss * pipSize

else if strategy.opentrades == 0
    stopLoss := na

if barstate.isconfirmed and strategy.opentrades > 0
    if strategy.opentrades.size(strategy.opentrades- 1) > 0
        if close < stopLoss
            strategy.close("Long")
        stopLoss := (close - input_stop_loss * pipSize) > stopLoss ? close - input_stop_loss * pipSize : stopLoss
    else if strategy.opentrades.size(strategy.opentrades - 1) < 0
        if close > stopLoss
            strategy.close("Short")
        stopLoss := (close + input_stop_loss * pipSize) < stopLoss ? close + input_stop_loss * pipSize : stopLoss

// Check if position is closed (Print label if true)
is_pos_closed = (strategy.position_size[1] != 0 and strategy.position_size == 0)
is_short = strategy.position_size < 0
is_long = strategy.position_size > 0

if is_pos_closed and is_short[1]
    label.new(bar_index, low, "ðŸŸ¢ Close Short", color = #9cff87, style = label.style_label_up)

else if is_pos_closed and is_long[1]
    label.new(bar_index, high, "ðŸ”´ Close Long", color = #f9396a, style = label.style_label_down, textcolor = color.white)

fill(tema2Plot, tema1Plot, math.max(tema2,tema1), tema2,color.new(#9cff87, 85), color.new(#9cff87, 70))
fill(tema2Plot, tema1Plot, tema2, math.min(tema2,tema1),color.new(#f9396a, 70), color.new(#f9396a, 85))

plot(stopLoss, color=#f9396a, style = plot.style_cross, linewidth = 2, title = "Trail Stop")
```