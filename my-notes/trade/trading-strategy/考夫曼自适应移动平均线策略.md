
[Trading Strategies & Indicators Built by TradingView Community](https://www.tradingview.com/script/rm6K3yV6-Kaufman-Adaptive-Moving-Average-KAMA-Strategy-TradeDots/)

![[Pasted image 20240621173639.png]]

“考夫曼自适应移动平均线 （KAMA） 策略”是一个趋势跟踪系统，它利用了考夫曼自适应移动平均线 （KAMA） 的自适应特性。该策略的特点是能够根据市场波动动态调整，通过最大限度地减少通常与简单移动平均线 （SMA） 相关的虚假和延迟信号的影响来提高交易准确性。

## How it works

该策略以使用 Kaufman 自适应移动平均线 （KAMA） 指标为中心，该指标以卓越的平滑技术完善了指数移动平均线 （EMA） 的原理。  
  
KAMA通过“效率比（ER）”对市场价格变化的响应能力而与众不同。该比率的计算方法是将最近的绝对净价格变化除以指定时期内绝对价格变化的累积总和。生成的 ER 值介于 0 和 1 之间，其中 0 表示市场噪音高，1 表示市场动能更强。  
  
使用 ER，我们可以得到使用以下公式得出的移动平均线的平滑常数 （SC）：

```pascal
fastest = 2/(fastma_length + 1)  
slowest = 2/(slowma_length + 1)  
SC =  math.pow((ER * (fastest-slowest) + slowest), 2)
```

然后，通过将 SC 应用于当前价格与之前 KAMA 之间的差额来计算 KAMA 线。

## Application

对于进入多头头寸，当有 10 条连续上升的 KAMA 线序列时，此策略将初始化。相反，连续 10 条下降的 KAMA 线会触发多头头寸的卖单。同样的逻辑也适用于空头头寸。


## Default setup

佣金：0.01%  
初始资本：$10,000  
每笔交易净值：80%  
  
建议用户调整和个性化此交易策略，以更好地匹配他们的个人交易偏好和风格。


![[Pasted image 20240621174015.png]]


## Script

```pascal
// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © tradedots

//@version=5
strategy("Kaufman Adaptive Moving Average (KAMA) Strategy [TradeDots]", overlay = true, max_lines_count = 500, max_boxes_count = 100, initial_capital = 10000, currency = currency.USD, commission_type = strategy.commission.percent, commission_value = 0.01, default_qty_type = strategy.percent_of_equity, default_qty_value = 80)

// KAMA Setup
lookback_period = input.int(10, title="Length", group="KAMA")
ma_source = input(close, "KAMA Soruce", group="KAMA")
fastma_length = input.int(5, "Fast MA Legnth", minval=1,group="KAMA")
slowma_length = input.int(50, "Slow MA Legnth", minval=1,group="KAMA")

// Strategy Setup
input_order_direction = input.string("Long", options = ["Long", "Short", "Long and Short"], title = "Order Direction", group = "Strategy")
kama_rising_perid = input.int(10, title="Rising Period", group="Strategy")
kama_falling_perid = input.int(10, title="Falling Period", group="Strategy")

pine_kama(source) =>
    price_change = math.abs(source - source[lookback_period])
    sum_price_change = math.sum(math.abs(source - source[1]), lookback_period)
    fastest = 2/(fastma_length + 1)
    slowest = 2/(slowma_length + 1)
    ER = price_change / sum_price_change
    SC =  math.pow((ER * (fastest-slowest) + slowest), 2)

    alpha = SC
    sum = 0.0
    sum := na(sum[1]) ? source : sum[1] + SC * (close - nz(sum[1]))

kama = pine_kama(ma_source)

if ta.rising(kama, kama_rising_perid)
    strategy.close("Short")
    if input_order_direction != "Short" and strategy.opentrades == 0
        strategy.entry("Long", strategy.long)
        label.new(bar_index, low, "🟢 Long", color = #9cff87, style = label.style_label_up)

else if ta.falling(kama, kama_falling_perid) 
    strategy.close("Long")
    if input_order_direction != "Long" and strategy.opentrades == 0
        strategy.entry("Short", strategy.short)
        label.new(bar_index, high, "🔴 Short", color = #f9396a, style = label.style_label_down, textcolor = color.white)

// Check if position is closed (Print label if true)
is_pos_closed = (strategy.position_size[1] != 0 and strategy.position_size == 0)
is_short = strategy.position_size < 0
is_long = strategy.position_size > 0

if is_pos_closed and is_short[1] 
    label.new(bar_index, low, "🟢 Close Short", color = #9cff87, style = label.style_label_up)

else if is_pos_closed and is_long[1]
    label.new(bar_index, high, "🔴 Close Long", color = #f9396a, style = label.style_label_down, textcolor = color.white)

closelinePlot = plot(close, "", display = display.none)
kamaPlot = plot(kama, title = "KAMA", color = #ffffff)
fill(kamaPlot, closelinePlot, math.max(kama,close), kama,color.new(#9cff87, 75), color.new(#9cff87, 60))
fill(kamaPlot, closelinePlot, kama, math.min(kama,close),color.new(#f9396a, 60), color.new(#f9396a, 75))


```