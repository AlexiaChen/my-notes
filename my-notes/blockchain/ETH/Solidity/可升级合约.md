
## 编写可升级合约

当使用OpenZeppelin Upgrades与可升级合约一起工作时，在编写你的Solidity代码时，有一些小的注意事项需要牢记。

值得一提的是，这些限制源于EVM的工作方式，适用于所有使用可升级合约的项目，而不仅仅是OpenZeppelin Upgrades。

### Initializers

您可以用OpenZeppelin Upgrades使用您的Solidity合约，而不需要做任何修改，除了它们的constructor。由于基于代理（proxy）的可升级性系统的要求，在可升级合约中不能使用constructpr。要了解这一限制背后的原因，请前往proxy [[#代理升级模式]]


这意味着，当使用OpenZeppelin Upgrades的合约时，你需要把它的constructor改成一个普通的函数，通常命名为`initialize`，在那里你可以运行所有的设置逻辑。

```d
// NOTE: Do not use this code snippet, it's incomplete and has a critical vulnerability!

pragma solidity ^0.6.0;

contract MyContract {
    uint256 public x;

    function initialize(uint256 _x) public {
        x = _x;
    }
}
```

然而，虽然 Solidity 确保constructor在合约的生命周期内只被调用一次，但普通函数可以被多次调用。为了防止一个合约被多次初始化，你需要添加一个检查，以确保初始化函数只被调用一次。

```d
// contracts/MyContract.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

contract MyContract {
    uint256 public x;
    bool private initialized;

    function initialize(uint256 _x) public {
        require(!initialized, "Contract instance has already been initialized");
        initialized = true;
        x = _x;
    }
}
```

由于这种模式在编写可升级的合约时非常常见，OpenZeppelin合约提供了一个`Initializable`的基类合约，它有一个`initializer`的modifier来处理这个问题。

```d
// contracts/MyContract.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

contract MyContract is Initializable {
    uint256 public x;

    function initialize(uint256 _x) public initializer {
        x = _x;
    }
}
```

constructor和普通函数的另一个区别是，Solidity 会自动调用合约的所有父类的构造器（类似于java，C++这样）。当写一个initializer时，你需要特别注意**手动**调用所有父合约的initializer。注意，即使使用继承，initializer modifier也只能被调用一次，所以父合约应该使用onlyInitializing modifier。

```d
// contracts/MyContract.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

contract BaseContract is Initializable {
    uint256 public y;

    function initialize() public onlyInitializing {
        y = 42;
    }
}

contract MyContract is BaseContract {
    uint256 public x;

    function initialize(uint256 _x) public initializer {
        BaseContract.initialize(); // Do not forget this call!
        x = _x;
    }
}
```

#### 使用可升级合约库

请记住，这个限制不仅影响你的合约，也影响你从库中导入的合约。例如考虑OpenZeppelin合约中的ERC20 https://github.com/OpenZeppelin/openzeppelin-contracts/blob/6be5ffe54fc8982b7c1d0f45d4cb7bbd313107cd/contracts/token/ERC20/ERC20.sol#L33 ：该合约在其构造函数中初始化了代币的名称、符号和小数。

```d
// @openzeppelin/contracts/token/ERC20/ERC20.sol
pragma solidity ^0.6.0;

  ...

contract ERC20 is Context, IERC20 {

  ...

    string private _name;
    string private _symbol;
    uint8 private _decimals;

    constructor (string memory name, string memory symbol) public {
        _name = name;
        _symbol = symbol;
        _decimals = 18;
    }

  ...
}
```

这意味着你不应该在你的OpenZeppelin升级项目中使用这些合约。相反，请确保使用`@openzeppelin/contracts-upgradeable`，它是OpenZeppelin合约的官方fork，已经被修改为使用initializer而不是constructor。看看在`@openzeppelin/contracts-upgradeable`中的`ERC20Upgradeable`是什么样子。
https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/c0b9fd6e81c00000dc0749aa116a7735f0d35fe3/contracts/token/ERC20/ERC20Upgradeable.sol#L34

```d
// @openzeppelin/contracts-upgradeable/contracts/token/ERC20/ERC20Upgradeable.sol
pragma solidity ^0.6.0;
  ...
contract ERC20Upgradeable is Initializable, ContextUpgradeable, IERC20Upgradeable {
  ...
    string private _name;
    string private _symbol;
    uint8 private _decimals;

    function __ERC20_init(string memory name, string memory symbol) internal initializer {
        __Context_init_unchained();
        __ERC20_init_unchained(name, symbol);
    }

    function __ERC20_init_unchained(string memory name, string memory symbol) internal initializer {
        _name = name;
        _symbol = symbol;
        _decimals = 18;
    }
  ...
}
```


无论是使用OpenZeppelin合约还是其他智能合约库，都要确保软件包被设置为处理可升级合约。

了解更多关于OpenZeppelin合约中的可升级合约。[Using with Upgrades - OpenZeppelin Docs](https://docs.openzeppelin.com/contracts/4.x/upgradeable) 

#### 避免初始值在成员中声明

Solidity 允许在合约中声明字段时为它们定义初始值。

```d
contract MyContract {
    uint256 public hasInitialValue = 42; // equivalent to setting in the constructor
}
```

这相当于在constructor中设置这些值，因此，对于可升级的合约来说是不起作用的。请确保所有的初始值都在initializer函数中设置，如下图所示；否则，任何可升级的实例都不会有这些字段的设置。

```d
contract MyContract is Initializable {
    uint256 public hasInitialValue;

    function initialize() public initializer {
        hasInitialValue = 42; // set initial value in initializer
    }
}
```

> 注意： 定义constant状态变量仍然是可以的，因为编译器不会为这些变量保留一个storage slot，每一次出现都会被相应的常量表达式取代。因此，下面的内容在OpenZeppelin升级版中仍然有效。

```d
contract MyContract {
    uint256 public constant hasInitialValue = 42; // define as constant
}
```


#### 初始化合约的实现

不要让实现合约未被初始化。一个未初始化的实现合约可以被攻击者接管，这可能会影响Proxy。为了防止实现合约被使用，你应该在构造函数中调用`_disableInitializers`函数，以便在部署时自动锁定它。

```d
/// @custom:oz-upgrades-unsafe-allow constructor
constructor() {
    _disableInitializers();
}
```


### 从你的合约代码创建新的实例(instances)

当从您的合约代码中创建一个新的合约实例时，这些创建直接由Solidity处理，而不是由OpenZeppelin upgrades处理，这意味着这些合约将不可升级。

例如，在下面的例子中，即使MyContract被部署为可升级的，创建的代币合约也不是可升级的合约。

```d
// contracts/MyContract.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract MyContract is Initializable {
    ERC20 public token;

    function initialize() public initializer {
        token = new ERC20("Test", "TST"); // This contract will not be upgradeable
    }
}
```

如果你想让ERC20实例可以升级，最简单的方法是简单地接受该合约的实例作为参数，并在创建后注入它。

```d
// contracts/MyContract.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC20/IERC20Upgradeable.sol";

contract MyContract is Initializable {
    IERC20Upgradeable public token;

    function initialize(IERC20Upgradeable _token) public initializer {
        token = _token;
    }
}
```

### 潜在的不安全的操作

当使用可升级的智能合约时，你将始终与合约实例互动，而不是与底层逻辑合约互动。然而，没有什么能阻止恶意行为者直接向逻辑合约发送交易。这并不构成威胁，因为对逻辑合约状态的任何改变都不会影响你的合约实例，因为逻辑合约的存储从未在你的项目中使用。

然而，有一个例外。如果对逻辑合约的直接调用触发了`selfdestruct`操作，那么逻辑合约将被销毁，而你所有的合约实例最终将委托所有的调用到一个没有任何代码的地址。这将有效地破坏你项目中的所有合约实例。

如果逻辑合约包含一个`delegatecall`操作，也可以达到类似的效果。如果可以使合约`delegatecall`到一个包含`selfdestruct`功能的恶意合约中，那么调用的合约将被销毁。

因此，不允许在你的合约中使用`selfdestruct`或`delegatecall`。

### 修改你的合约

在编写新版本的合约时，无论是由于新的功能还是bug的修复，都要遵守一个额外的限制：你不能改变合约状态变量的声明顺序，也不能改变其类型。你可以通过了解我们的代理来阅读更多关于这个限制背后的原因。[Proxy Upgrade Pattern - OpenZeppelin Docs](https://docs.openzeppelin.com/upgrades-plugins/1.x/proxies) 

> 警告： 违反这些存储布局限制中的任何一项，都会导致升级后的合约版本的存储值被混淆，并可能导致你的应用程序出现关键错误。

这意味着，如果你有一个看起来像这样的初始合约

```d
contract MyContract {
    uint256 private x;
    string private y;
}
```

然后你就不能像下面那样改变变量的类型

```d
contract MyContract {
    string private x;
    string private y;
}
```

或者改变它们的声明顺序：

```d
contract MyContract {
    string private y;
    uint256 private x;
}
```

或者在现有变量之前引入一个新的变量。

```d
contract MyContract {
    bytes private a;
    uint256 private x;
    string private y;
}
```

或者删除一个现有的变量。

```d
contract MyContract {
    string private y;
}
```

如果你需要引入一个新的变量，确保你总是在最后

```d
contract MyContract {
    uint256 private x;
    string private y;
    bytes private z;
}
```

请记住，如果你重命名一个变量，那么升级后它将保持与以前相同的值。如果新变量在语义上与旧变量相同，这可能是所希望的行为。

```d
contract MyContract {
    uint256 private x;
    string private z; // starts with the value from `y`
}
```

而如果你从合约的末尾删除了一个变量，请注意存储不会被清空。随后添加新变量的更新将导致该变量从被删除的变量中读取剩余的值。

```d
contract MyContract {
    uint256 private x;
}

// upgrade to below

contract MyContract {
    uint256 private x;
    string private z; // starts with the value from `y`
}
```

请注意，你也可能因为改变了你的合约的父合约而无意中改变了合约的存储变量。例如，如果你有以下合约   。 然后，通过调换父类合约的声明顺序来修改MyContract，或者引入新的父类合约，将改变变量的实际存储方式。

```d
contract A {
    uint256 a;
}


contract B {
    uint256 b;
}


contract MyContract is A, B {}

// upgrade to below

contract MyContract is B, A {}
```

如果子合约有任何自己的变量，你也不能向父类合约添加新的变量。鉴于以下情况。

```d
contract Base {
    uint256 base1;
}


contract Child is Base {
    uint256 child;
}

// upgrade to below

contract Base {
    uint256 base1;
    uint256 base2;
}
```

然后，变量base2将被分配到子合约在前一个版本中的slot中。一个解决这个问题的方法是在基础合约中声明未使用的变量或存储间隙，作为 "保留 "这些slot的一种手段，你可能想在将来扩展。请注意，这个技巧并不涉及增加gas的使用。

### 存储间隙

存储间隙是一种在父类合约中保留storage slot的惯例，允许该合约的未来版本用完这些槽，而不影响子合约的存储布局。

要创建一个存储间隙，需要在基础合约中声明一个固定大小的数组，其中有一个初始数量的slot。这可以是一个uint256的数组，这样每个元素就保留了一个32字节的slot。使用命名惯例`__gap`，这样OpenZeppelin Upgrades就能识别这个间隙。

```d
contract Base {
    uint256 base1;
    uint256[49] __gap;
}

contract Child is Base {
    uint256 child;
}
```

如果Base后来被修改以增加额外的变量，请从存储间隙中减少适当的槽数，记住Solidity关于如何pack连续项目的规则。[Layout of State Variables in Storage — Solidity 0.8.18 documentation (soliditylang.org)](https://docs.soliditylang.org/en/latest/internals/layout_in_storage.html#layout-of-state-variables-in-storage) 比如说。

```d
contract Base {
    uint256 base1;
    uint256 base2; // 32 bytes
    uint256[48] __gap;
}
```

```d
contract Base {
    uint256 base1;
    address base2; // 20 bytes
    uint256[48] __gap; // array always starts at a new slot
}
```

```d
contract Base {
    uint256 base1;
    uint128 base2a; // 16 bytes
    uint128 base2b; // 16 bytes - continues from the same slot as above
    uint256[48] __gap;
}
```

为了帮助确定新版本合约中适当的存储间隙大小，你可以简单地使用`upgradeProxy`尝试升级，或者直接使用`validateUpgrade`运行验证（见Hardhat或Truffle的文档）。如果存储间隙没有被正确减少，你会看到一个错误信息，表明存储间隙的预期大小。

## 代理升级模式

这篇文章描述了 "非结构化存储 "代理模式，这是OpenZeppelin升级的基本构建块。

> 要想更深入地阅读，请看我们的代理模式博客文章 [Proxy Patterns - OpenZeppelin blog](https://blog.openzeppelin.com/proxy-patterns/) ，其中讨论了代理的必要性，对这个问题进行了更多的技术细节，阐述了OpenZeppelin升级所考虑的其他可能的代理模式，以及更多。

### 为什么要升级一个合约

根据设计，智能合约是不可改变的。另一方面，软件质量在很大程度上取决于升级和修补源代码的能力，以产生迭代版本。即使基于区块链的软件从该技术的不可更改性中获得了巨大的利益，但仍然需要一定程度的可变性来修复错误和潜在的产品改进。OpenZeppelin Upgrades解决了这一明显的矛盾，它为智能合约提供了一个易于使用、简单、强大和可选择的升级机制，可以由任何类型的治理机构控制，无论是多签名钱包、一个简单地址还是复杂的DAO。

### 通过代理模式升级

其基本思想是使用代理进行升级。第一个合约是一个简单的包装或 "代理"，用户直接与之互动，并负责将交易转发给第二个合约，其中包含逻辑。需要理解的关键概念是，业务逻辑合约可以被替换，而代理，或接入点永远不会改变（有点像网关）。这两个合约仍然是不可改变的，因为它们的代码不能被改变，但业务逻辑合约可以简单地被另一个合约所取代。因此，封装器可以指向不同的逻辑实现，这样一来，软件就 "升级 "了。

```
User ---- tx ---> Proxy ----------> Implementation_v0
                     |
                      ------------> Implementation_v1
                     |
                      ------------> Implementation_v2
```

### 代理转发

代理需要解决的最直接的问题是，代理如何公开业务逻辑合约的整个接口，而不需要对整个逻辑合约的接口进行一对一的映射。这样就很难维护，容易出错，而且会使接口本身无法升级。因此，需要一种动态转发机制。这种机制的基本原理在下面的代码中呈现。

```d
assembly {
  let ptr := mload(0x40)

  // (1) copy incoming call data
  calldatacopy(ptr, 0, calldatasize)

  // (2) forward call to logic contract
  let result := delegatecall(gas, _impl, ptr, calldatasize, 0, 0)
  let size := returndatasize

  // (3) retrieve return data
  returndatacopy(ptr, 0, size)

  // (4) forward return data back to caller
  switch result
  case 0 { revert(ptr, size) }
  default { return(ptr, size) }
}
```


这段代码可以放在代理的fallback函数中 https://solidity.readthedocs.io/en/v0.6.12/contracts.html#fallback-function ，并将任何对带有任何参数集的函数的调用转发给业务逻辑合约，而不需要知道逻辑合约的任何具体接口。实质上，（1）调用数据被复制到内存中，（2）调用被转发到逻辑合约，（3）从调用到逻辑合约的返回数据被检索，（4）返回数据被转发回调用者。

需要注意的一个非常重要的事情是，该代码利用了EVM的delegatecall操作码，该操作码在调用者的状态上下文下执行callee(被调者)的代码。也就是说，业务逻辑合约控制着代理的状态（callee控制着caller的状态），而逻辑合约的状态是没有意义的。因此，代理不仅仅是在逻辑合约之间转发交易，而且还代表了这一对（代理和业务逻辑这两个合约）的状态。状态在代理中，逻辑在代理所指向的特定实现中。

### 非结构化存储代理

在使用代理时，很快就会出现一个问题，这与代理合约中变量的存储方式有关。假设代理将业务逻辑合约的地址存储在其唯一的变量`address public _implementation`;中。现在，假设逻辑合约是一个基本标记，其第一个变量是`address public \_owner`。这两个变量的大小都是32字节，就EVM所知，占据了代理调用的结果执行流程的第一个slot。当逻辑合约写到`_owner`时，它是在代理的状态范围内进行的，实际上是写到了`_implementation`。这个问题可以被称为 "存储碰撞"。

```txt
|Proxy                     |Implementation           |
|--------------------------|-------------------------|
|address _implementation   |address _owner           | <=== Storage collision!
|...                       |mapping _balances        |
|                          |uint256 _supply          |
|                          |...                      |

```

有许多方法可以克服这个问题，而OpenZeppelin upgrades实现的 "非结构化存储 "方法的工作原理如下。它没有将`_implementation`地址存储在代理的第一个storage slot，而是选择了一个伪随机slot。这个slot是足够随机的，以至于逻辑合约在同一slot中声明一个变量的概率可以忽略不计。在代理的storage slot位置随机化的原则同样可以用于代理可能拥有的任何其他变量，例如admin地址（允许更新`_implementation`的值），等等。

```txt
|Proxy                     |Implementation           |
|--------------------------|-------------------------|
|...                       |address _owner           |
|...                       |mapping _balances        |
|...                       |uint256 _supply          |
|...                       |...                      |
|...                       |                         |
|...                       |                         |
|...                       |                         |
|...                       |                         |
|address _implementation   |                         | <=== Randomized slot.
|...                       |                         |
|...                       |                         |
```

以下是如何实现随机存储的一个例子, 是照抄的 EIP 1967来的 [EIP-1967: Proxy Storage Slots (ethereum.org)](https://eips.ethereum.org/EIPS/eip-1967)

```d
bytes32 private constant implementationPosition = bytes32(uint256( keccak256('eip1967.proxy.implementation')) - 1 ));
```

因此，业务逻辑合约不需要关心会重写代理的任何变量。其他面临这个问题的代理实现通常意味着让代理知道逻辑合约的存储结构并适应它，或者让逻辑合约知道代理的存储结构并适应它。这就是为什么这种方法被称为 "非结构化存储"；两个合约都不需要关心另一个合约的结构。


### 不同实现版本之间的存储碰撞

如前所述，非结构化的方法避免了逻辑合约和代理之间的存储碰撞。然而，不同版本的逻辑合约之间可能会发生存储碰撞。在这种情况下，想象一下，逻辑合约的第一个实现在第一个storage slot中存储了地址public \_owner，而升级后的逻辑合约在同一个第一个slot中存储了地址public \_lastContributor。当升级后的逻辑合约试图写到_lastContributor变量时，它将使用与之前存储_owner的值相同的存储位置，并覆盖它

不正确的存储声明。

```txt
|Implementation_v0   |Implementation_v1        |
|--------------------|-------------------------|
|address _owner      |address _lastContributor | <=== Storage collision!
|mapping _balances   |address _owner           |
|uint256 _supply     |mapping _balances        |
|...                 |uint256 _supply          |
|                    |...                      |
```

正确的做法:

```txt
|Implementation_v0   |Implementation_v1        |
|--------------------|-------------------------|
|address _owner      |address _owner           |
|mapping _balances   |mapping _balances        |
|uint256 _supply     |uint256 _supply          |
|...                 |address _lastContributor | <=== Storage extension.
|                    |...                      |
```

非结构化存储代理机制并不能防止这种情况。这取决于用户是否让新版本的逻辑合约扩展以前的版本，或以其他方式保证存储层次结构总是被附加到但不被修改。然而，OpenZeppelin升级会检测到这种碰撞，并适当地警告开发者。

> 说下我的个人看法，以上就是解释了为什么一些状态变量的声明顺序不能被打乱，只有追加。不然会有冲突，也解释了父类继承的顺序也不能被打乱。


### constructor注意事项

在 Solidity 中，位于constructor内的代码或全局变量声明的一部分不属于部署的合约的运行时字节码的一部分。当合约实例被部署时， 这段代码只被执行一次。因此，业务逻辑合约的constructor中的代码永远不会在代理的状态上下文下被执行。换句话说，代理对constructor的存在是完全无视的。对代理来说，这就好像它们不存在一样。

不过这个问题很容易解决。逻辑合约应该把构造函数中的代码移到一个普通的 "initializer "函数中，并且在代理链接到这个逻辑合约时调用这个函数。需要特别注意这个initializer函数，以便它只能被调用一次，这也是一般编程中constructor的属性之一。

这就是为什么当我们使用OpenZeppelin Upgrades创建一个代理时，你可以提供initializer函数的名称并传递参数。

为了确保`initialize`函数只能被调用一次，我们使用了一个简单的moddifier。OpenZeppelin Upgrades通过一个可以扩展的合约提供这个功能。

```d
// contracts/MyContract.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.6.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

contract MyContract is Initializable {
    function initialize(
        address arg1,
        uint256 arg2,
        bytes memory arg3
    ) public payable initializer {
        // "constructor" code...
    }
}
```

注意该合约是如何扩展`Initializable`并实现它所提供的`initializer`的。

### 透明代理和功能冲突

如前几节所述，可升级的合约实例（或代理）通过委托所有调用逻辑合约来工作。然而，代理需要一些自己的函数，比如upgradeTo(address)来升级到一个新的实现。这就引出了一个问题：如果逻辑合约也有一个名为upgradeTo(address)的函数，该如何进行：在调用该函数时，调用者是想调用代理还是逻辑合约？

> 注意：冲突也可能发生在具有不同名称的函数之间。作为合约的公共ABI的一部分，每个函数在字节码层面上都由一个4字节的标识符来识别。这个标识符取决于函数的名称和算术，但由于它只有4个字节，有可能两个不同名称的函数最终会有相同的标识符。Solidity 编译器会跟踪这种情况在同一合约内发生的情况，但不跟踪在不同合约之间发生的碰撞，例如在代理和它的业务逻辑合约之间。阅读这篇文章 [Malicious backdoors in Ethereum Proxies | by Patricio Palladino | Nomic Foundation | Medium](https://medium.com/nomic-foundation-blog/malicious-backdoors-in-ethereum-proxies-62629adf3357) 以获得更多关于此的信息。

OpenZeppelin Upgrades处理这个问题的方式是通过透明代理模式。一个透明的代理将根据调用者的地址（即msg.sender）来决定哪些调用被委托给底层的业务逻辑合约。

- 如果调用者是代理的admin（有权利升级代理合约的地址），那么代理将不委托任何调用，只回答它理解的任何消息。

- 如果调用者是任何其他地址，代理将总是委托调用，不管它是否符合代理的一个功能。

假设一个具有owner()和upgradeTo()函数的代理，委托调用一个具有owner()和transfer()函数的ERC20合约，下表涵盖了所有情况。

![[Pasted image 20221104151246.png]]

幸运的是，OpenZeppelin Upgrades考虑到了这种情况，并创建了一个中间的ProxyAdmin合约，负责你通过Upgrades plugins创建的所有代理。即使你从你的节点的默认账户调用`deploy`命令，ProxyAdmin合约将是你所有代理的实际管理员。这意味着，你将能够从你的任何一个节点的账户与代理进行互动，而不必担心透明代理模式的细微差别。只有从 Solidity 创建代理的高级用户才需要了解透明代理模式。

### 总结

任何使用可升级合约的开发者都应该熟悉本文所述的代理方式。最后，这个概念非常简单，OpenZeppelin Upgrades的设计是为了封装所有的代理机制，使你在开发项目时需要记住的东西减少到最小。这一切都归结为以下的清单。

- 对什么是代理有一个基本的了解
- 始终扩展storage而不是修改它
- 确保你的合约使用initializer函数而不是constructor

此外，当这个列表中的某个项目出现问题时，OpenZeppelin upgrades会让你知道。

## 升级合约

使用OpenZeppelin upgrades plugins部署的智能合约可以被升级以修改其代码，同时保留其地址、状态和余额。

### 什么是可升级

以太坊的智能合约默认是不可改变的。一旦你创建了它们，就没有办法改变它们，有效地充当了参与者之间不可违背的“合约“。  
  
然而，在某些情况下，能够修改它们是可取的。想想两方之间的传统合约：如果他们都同意改变它，他们将能够这样做。在以太坊上，他们可能希望改变一个智能合约，以修复他们发现的错误（这甚至可能导致黑客窃取他们的资金！），增加额外的功能，或只是改变它所执行的规则。  
  
以下是你需要做的事情，以修复一个你无法升级的合约中的错误。  
  
- 部署一个新版本的合约 
- 手动将所有的状态从旧的合约迁移到新的合约上（这在gas费方面可能非常昂贵！）
- 更新所有与旧合约互动的合约，以使用新合约的地址
- 联系你的所有用户，说服他们开始使用新的部署的合约（并处理两个合约同时使用的情况，因为用户迁移的速度很慢）
  
为了避免经历这种混乱，我们已经将合约升级直接建立在我们的插件中。这使我们**能够改变合约的代码，同时保留状态、余额和地址**。让我们看看它的作用。  

### 使用Upgrades plugins来升级

每当你在OpenZeppelin升级插件中使用`deployProxy`部署一个新的合约时，该合约实例可以在以后被升级。默认情况下，只有部署最初合约的地址有权利升级它。

`deployProxy`将创建以下交易：

- 部署业务逻辑实现合约（我们的Box合约）
- 部署ProxyAdmin合约（我们的代理的管理员）。
- 部署代理合约并运行任何初intializer函数。

让我们看看它是如何工作的，通过部署我们的Box合约的可升级版本，使用与我们先前部署时相同的设置。[Deploying and interacting with smart contracts - OpenZeppelin Docs](https://docs.openzeppelin.com/learn/deploying-and-interacting#deploying-a-smart-contract)

```d
// contracts/Box.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Box {
    uint256 private _value;

    // Emitted when the stored value changes
    event ValueChanged(uint256 value);

    // Stores a new value in the contract
    function store(uint256 value) public {
        _value = value;
        emit ValueChanged(value);
    }

    // Reads the last stored value
    function retrieve() public view returns (uint256) {
        return _value;
    }
}
```

我们首先需要安装Upgrades插件。

安装Hardhat Upgrades插件。

```bash
npm install --save-dev @openzeppelin/hardhat-upgrades
```

然后我们需要配置Hardhat以使用我们的`@openzeppelin/hardhat-upgrades`插件。要做到这一点，在你的hardhat.config.js文件中添加该插件，如下所示。

```js
// hardhat.config.js
...
require('@nomiclabs/hardhat-ethers');
require('@openzeppelin/hardhat-upgrades');
...
module.exports = {
...
};
```

为了升级像Box这样的合约，我们需要首先将其部署为可升级的合约，这与我们到目前为止看到的部署程序不同。我们将通过调用带有42值的存储来初始化我们的Box合约。

Hardhat目前没有一个原生的部署系统，相反，我们使用脚本来部署合约。

我们将创建一个脚本，使用`deployProx`y部署我们的可升级Box合约。我们将把这个文件保存为 `scripts/deploy_upgradeable_box.js`。


```d
// scripts/deploy_upgradeable_box.js
const { ethers, upgrades } = require('hardhat');

async function main () {
  const Box = await ethers.getContractFactory('Box');
  console.log('Deploying Box...');
  const box = await upgrades.deployProxy(Box, [42], { initializer: 'store' });
  await box.deployed();
  console.log('Box deployed to:', box.address);
}

main();
```

然后我们就可以部署我们的可升级合约了。

使用`run`命令，我们可以将Box合约部署到开发网络中。

```bash
npx hardhat run --network localhost scripts/deploy_upgradeable_box.js

Deploying Box... Box deployed to: 0x9fE46736679d2D9a65F0992F2272dE9f3c7fa6e0
```

然后我们可以与我们的Box合约互动，以检索我们在初始化过程中存储的值。

我们将使用Hardhat console与我们升级后的Box合约进行交互。

我们需要指定我们部署Box合约时的代理合约的地址。

```txt
npx hardhat console --network localhost
Welcome to Node.js v12.22.1.
Type ".help" for more information.
> const Box = await ethers.getContractFactory('Box');
undefined
> const box = await Box.attach('0x9fE46736679d2D9a65F0992F2272dE9f3c7fa6e0');
undefined
> (await box.retrieve()).toString();
'42'
```

为了举例说明，假设我们想添加一个新的功能：一个可以增加存储在新版本Box中的值的函数。

```d
// contracts/BoxV2.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract BoxV2 {
    // ... code from Box.sol

    // Increments the stored value by 1
    function increment() public {
        _value = _value + 1;
        emit ValueChanged(_value);
    }
}
```

在创建 Solidity 文件之后，我们现在可以使用 `upgradeProxy` 函数升级我们先前部署的实例。

`upgradeProxy` 将创建以下交易。

- 部署实现合约（我们的 BoxV2 合约）
- 调用 ProxyAdmin 来更新代理合约，以使用新的实现。

我们将创建一个脚本，使用`upgradeProxy` [OpenZeppelin Hardhat Upgrades API - OpenZeppelin Docs](https://docs.openzeppelin.com/upgrades-plugins/1.x/api-hardhat-upgrades#upgrade-proxy) 将我们的Box合约升级到使用BoxV2。我们将把这个文件保存为`scripts/upgrade_box.js`。我们需要指定我们的代理合约的地址，从我们部署Box合约时开始。

```d
// scripts/upgrade_box.js
const { ethers, upgrades } = require('hardhat');

async function main () {
  const BoxV2 = await ethers.getContractFactory('BoxV2');
  console.log('Upgrading Box...');
  await upgrades.upgradeProxy('0x9fE46736679d2D9a65F0992F2272dE9f3c7fa6e0', BoxV2);
  console.log('Box upgraded');
}

main();
```

然后我们就可以部署我们的可升级合约了。

使用run命令，我们可以升级开发网络上的Box合约。

```txt
npx hardhat run --network localhost scripts/upgrade_box.js
Compiling 1 file with 0.8.4
Compilation finished successfully
Upgrading Box...
Box upgraded
```

完成了! 我们的Box实例已经升级到了最新版本的代码，同时保留了它的状态和之前的地址。我们不需要在新的地址上部署一个新的，也不需要手动将值从旧的Box复制到新的。

让我们通过调用新的incre函数来试试，并在之后检查值。

我们需要指定我们部署Box合约时的代理合约的地址。

```txt
npx hardhat console --network localhost
Welcome to Node.js v12.22.1.
Type ".help" for more information.
> const BoxV2 = await ethers.getContractFactory('BoxV2');
undefined
> const box = await BoxV2.attach('0x9fE46736679d2D9a65F0992F2272dE9f3c7fa6e0');
undefined
> await box.increment();
...
> (await box.retrieve()).toString();
'43'
```

这就是了! 请注意，在整个升级过程中，Box的value以及它的地址都被保留了下来。而且，无论你是在本地区块链、测试网还是主网工作，这个过程都是一样的。

让我们看看OpenZeppelin升级插件是如何完成的。

### 这样的升级方式的原理是什么

当你创建一个新的可升级合约实例时，OpenZeppelin upgrades plugins实际上会部署三个合约。

- 你所写的合约，这被称为包含业务逻辑的实现合约。
- 一个ProxyAdmin，作为代理的管理员。
- 一个业务逻辑实现合约的代理合约，这是你实际交互的合约。（类似于API gateway）

在这里，代理是一个简单的合约，它只是将所有的调用委托给一个执行合约。delegatecall与普通调用类似，只是所有的代码都是在调用者的上下文中执行的，而不是被调用者的。正因为如此，业务逻辑实现合约的代码中的transfer实际上会转移代理合约的余额，对合约storage的任何读或写都会从代理自己的storage中读或写。

这使得我们可以将合约的状态和代码解耦：代理持有状态，而执行合约提供代码。它还允许我们通过让代理委托给不同的实现合约来改变代码。

然后，升级包括以下步骤。

- 部署新的实现合约。
- 向代理发送一个交易，将其实现地址更新为新的实现合约的地址。

> 你可以有多个代理使用相同的实现合约，所以如果你计划部署同一合约的多个副本，你可以使用这种模式来节省gas。

智能合约的任何用户总是与代理合约进行互动，而代理合约永远不会改变其自身的地址。这允许你推出一个升级或修复一个错误，而不要求你的用户改变他们的任何东西 - 他们只是一如既往地与同一个地址进行互动。更多更深入的细节参考 [[#代理升级模式]]

### 合约升级的限制

虽然任何智能合约都可以被升级，但Solidity语言的一些限制需要被解决。这些限制在编写初始版本的合约和我们要升级的版本时都会出现。

#### 初始化

可升级的合约不能有一个constructor。为了帮助你运行初始化代码，OpenZeppelin合约提供了`Initializable`基础合约，允许你将一个方法标记为initializer，确保它只能运行一次。

作为一个例子，让我们写一个带有intializer的新版本的Box合约，存储一个管理员的地址，他将是唯一被允许改变其内容的人。

```d
// contracts/AdminBox.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";

contract AdminBox is Initializable {
    uint256 private _value;
    address private _admin;

    // Emitted when the stored value changes
    event ValueChanged(uint256 value);

    function initialize(address admin) public initializer {
        _admin = admin;
    }

    /// @custom:oz-upgrades-unsafe-allow constructor
    constructor() initializer {}

    // Stores a new value in the contract
    function store(uint256 value) public {
        require(msg.sender == _admin, "AdminBox: not admin");
        _value = value;
        emit ValueChanged(value);
    }

    // Reads the last stored value
    function retrieve() public view returns (uint256) {
        return _value;
    }
}
```

在部署这个合约时，我们需要指定initializer函数的名称（只有当名称不是默认的initialize时），并提供我们想使用的管理地址。

```js
// scripts/deploy_upgradeable_adminbox.js
const { ethers, upgrades } = require('hardhat');

async function main () {
  const AdminBox = await ethers.getContractFactory('AdminBox');
  console.log('Deploying AdminBox...');
  const adminBox = await upgrades.deployProxy(AdminBox, ['0xACa94ef8bD5ffEE41947b4585a84BdA5a3d3DA6E'], { initializer: 'initialize' });
  await adminBox.deployed();
  console.log('AdminBox deployed to:', adminBox.address);
}

main();
```


为了所有的实际目的，initializer就像一个constructor。然而，请记住，由于它是一个普通的函数，你将需要手动调用所有父类合约的initializer（如果有的话）。

你可能已经注意到，我们包含了一个constructor以及一个initializer。这个constructor的作用是让实现合约处于初始化状态，这也是对某些潜在攻击的一种缓解。

要了解更多关于这个问题以及编写可升级契约时的其他注意事项，请查看我们的编写可升级契约指南。[[#编写可升级合约]]

#### 升级

由于技术上的限制，当你将一个合约升级到新版本时，你不能改变该合约的storage layout。

这意味着，如果你已经在合约中声明了一个状态变量，你不能删除它，改变它的类型，或者在它之前声明另一个变量。在我们的Box例子中，这意味着我们只能在value之后添加新的状态变量。

```d
// contracts/BoxV2.sol
contract BoxV2 {
    uint256 private _value;

    // We can safely add a new variable after the ones we had declared
    address private _owner;

    // ...
}
```

幸运的是，这种限制只影响到状态变量。你可以随心所欲地改变合约的函数和事件。

> 注意: 如果你不小心弄乱了合约的存储布局，升级插件会在你试图升级时警告你。

更多请参考 [[#修改你的合约]]

### 测试

为了测试可升级的合约，我们应该为业务逻辑实现合约创建单元测试，同时创建更高层次的测试，以测试通过代理的交互。我们可以在我们的测试中使用`deployProxy`，就像我们在部署时那样。

当我们想升级时，我们应该为新的业务逻辑实现合约创建单元测试，同时创建更高层次的测试，以测试在我们使用`upgradeProxy`升级后通过代理进行的交互，检查状态是否在不同的升级中保持。

### 可能的问题

在学习如何升级合约时，你可能会发现自己在本地环境中遇到合约冲突的情况。为了解决这个问题，可以考虑使用下面的步骤。

停止与`npx hardhat nod`e一起运行的节点ctrl+C。执行一次清理：`npx hardhat clean`。