
## 前言

大家知道，在公有区块链上，所有的交易都是明文公开的，甚至可以解析出交易对合约的调用输入参数。所有的数据如果不经过主动的加密，那么在区块链上都是透明的。比如，我们要做一个剪刀石头布的猜拳游戏，在以太坊的EVM上运行，你会怎么设计呢？因为EVM是单线程运行，也不允许合约方法的并发调用，本质上都是一个一个的来，比如Alice它出剪刀的交易被Bob截获了，Bob可以延迟一下，等看到Alice的结果再出锤子，那么对于后来的人，就有巨大的优势，100%可以作弊。有没有什么办法可以做到Alice和Bob都出结果了，但是对手方又看不到，等到要揭晓比赛结果的时候，才知道。

如果对密码学熟悉的人，即使不熟悉合约开发，肯定也不用参考别人的合约例子就能大概想到一些安全的做法。密码学无处不在，其实猜拳小游戏的，更实际的应用推广是用在电子投票之中，本质都是一样的，防止一些人跟风操作。这里的核心做法就是用密码学中的承诺方案(commitment scheme) [Commitment scheme - Wikipedia](https://en.wikipedia.org/wiki/Commitment_scheme) commitment方案无处不在，ZKP协议，secret sharing中都有用到。

commitment具有binding和hiding的特性，用这两个特性可以解决出拳的结果被隐藏（别人看不到，hiding），但是又不可更改,无法抵赖(binding)。等到两个人都出完拳的时候，在揭露答案。也就是校验commitment的过程。再深入一点的话，commitment的实现方案有很多，但是无论什么方案，都无法同时做到binding和hiding这两个特性发挥到极致，也就是同时支持完美binding和完美hiding。只能做到其中一个，也就是要么是完美binding，要么是完美hiding。要做到其中一个完美，另一个必须舍弃一些安全性。完美的定义就是理论上的不可能，比如完美binding就是，如果Alice对某个她选择的值做出了commitment，那么这个commitment是无法被修改的（即使Alice有无限的计算资源）。完美hiding的意思是，如果Alice不reveal她做出的commitment，那么Bob是不知道这个commitment里面的具体信息的（即使Bob有无限的计算资源）

所以说人话就是，commitment方案既要做到自己无法修改自己选择value，也要做到别人无法解密commitment以知道自己的value。只是两个特性各有取舍罢了。

## 正文

承诺有很多构造方式，我选择用最简单的Hash函数来构造承诺，因为这样的猜拳小游戏对安全的要求不是很高，而且出拳的间隔时间有限，作弊的情况就不太可能出现。不可能Alice出了拳，Bob等几天以后再出拳吧？电子投票也是同理，不然游戏，投票无法玩了。所以我们就对commitment的hiding和binding的安全性取舍并没有特殊偏好，随便用。

### 电子投票

![[Pasted image 20221107105339.png]]

这里的hash 函数用keccak256。只用了hash(vote) == commitment。

```d
pragma solidity ^0.4.7;

contract CommitRevealElection {
    // The two choices for your vote
    string public choice1;
    string public choice2;
    
    // Information about the current status of the vote
    uint public votesForChoice1;
    uint public votesForChoice2;
    uint public commitPhaseEndTime;
    uint public numberOfVotesCast = 0;

    // The actual votes and vote commits
    bytes32[] public voteCommits;
    mapping(bytes32 => string) voteStatuses; // Either `Committed` or `Revealed`
    
    // Events used to log what's going on in the contract
    event logString(string);
    event newVoteCommit(string, bytes32);
    event voteWinner(string, string);
    
    // Constructor used to set parameters for the this specific vote
    function CommitRevealElection(uint _commitPhaseLengthInSeconds, 
                                  string _choice1, 
                                  string _choice2) {
        if (_commitPhaseLengthInSeconds < 20) {
            throw;
        }
        commitPhaseEndTime = now + _commitPhaseLengthInSeconds * 1 seconds;
        choice1 = _choice1;
        choice2 = _choice2;
    }
    
    function commitVote(bytes32 _voteCommit) {
        if (now > commitPhaseEndTime) throw; // Only allow commits during committing period
        
        // Check if this commit has been used before
        bytes memory bytesVoteCommit = bytes(voteStatuses[_voteCommit]);
        if (bytesVoteCommit.length != 0) throw;
        
        // We are still in the committing period & the commit is new so add it
        voteCommits.push(_voteCommit);
        voteStatuses[_voteCommit] = "Committed";
        numberOfVotesCast ++;
        newVoteCommit("Vote committed with the following hash:", _voteCommit);
    }
    
    function revealVote(string _vote, bytes32 _voteCommit) {
        if (now < commitPhaseEndTime) throw; // Only reveal votes after committing period is over
        
        // FIRST: Verify the vote & commit is valid
        bytes memory bytesVoteStatus = bytes(voteStatuses[_voteCommit]);
        if (bytesVoteStatus.length == 0) {
            logString('A vote with this voteCommit was not cast');
        } else if (bytesVoteStatus[0] != 'C') {
            logString('This vote was already cast');
            return;
        }
        
        if (_voteCommit != keccak256(_vote)) {
            logString('Vote hash does not match vote commit');
            return;
        }
        
        // NEXT: Count the vote!
        bytes memory bytesVote = bytes(_vote);
        if (bytesVote[0] == '1') {
            votesForChoice1 = votesForChoice1 + 1;
            logString('Vote for choice 1 counted.');
        } else if (bytesVote[0] == '2') {
            votesForChoice2 = votesForChoice2 + 1;
            logString('Vote for choice 2 counted.');
        } else {
            logString('Vote could not be read! Votes must start with the ASCII character `1` or `2`');
        }
        voteStatuses[_voteCommit] = "Revealed";
    }
    
    function getWinner () constant returns(string) {
        // Only get winner after all vote commits are in
        if (now < commitPhaseEndTime) throw;
        // Make sure all the votes have been counted
        if (votesForChoice1 + votesForChoice2 != voteCommits.length) throw;
        
        if (votesForChoice1 > votesForChoice2) {
            voteWinner("And the winner of the vote is:", choice1);
            return choice1;
        } else if (votesForChoice2 > votesForChoice1) {
            voteWinner("And the winner of the vote is:", choice2);
            return choice2;
        } else if (votesForChoice1 == votesForChoice2) {
            voteWinner("The vote ended in a tie!", "");
            return "It was a tie!";
        }
    }
}
```

其中`bytes32 \_voteCommit` 是用keccak256生成的，至于投票的格式是1-\<voter-random-secret-nonce\>或 2-\<voter-random-secret-nonce\>。  前面的1或2就是投票的选项，选择1还是2。后面是一个随机的密码串，所以最终的公式是: 

$$
voteCommit = keccak256(choose value || random nonce)
$$

最终的调用就是`commitVote(voteCommit)`来提交你的承诺。

### 猜拳游戏

让我们来看看我们如何使用commitment方案在以太坊上正确建立一个剪刀石头布游戏。

让我们从我们的constructor和初始化值开始。我们将需要玩家的选择--石头、纸和剪刀。我们还将有一些阶段，以便函数只能在各自的阶段运行。最后，我们将创建一个结构来表示玩家的commitment，以及一些将在构造函数中设置的初始化值、一些状态变量和事件。

```d
contract RockPaperScissors {
    enum Choice {
        None,
        Rock,
        Paper,
        Scissors
    }

    enum Stage {
        FirstCommit,
        SecondCommit,
        FirstReveal,
        SecondReveal,
        Distribute
    }

    struct CommitChoice {
        address playerAddress;
        bytes32 commitment;
        Choice choice;
    }

    event Commit(address player);
    event Reveal(address player, Choice choice);
    event Payout(address player, uint amount);

    // Initialisation args
    uint public bet;
    uint public deposit;
    uint public revealSpan;

    // State vars
    CommitChoice[2] public players;
    uint public revealDeadline;
    Stage public stage = Stage.FirstCommit;

    constructor(uint _bet, uint _deposit, uint _revealSpan) public {
        bet = _bet;
        deposit = _deposit;
        revealSpan = _revealSpan;
    }
}
```

接下来，我们将组建一个提交（commit）函数，从检查开始。首先，我们将require该函数只在两个提交阶段中的一个阶段运行。接下来，我们可以确保适当的值与交易一起被发送，并送回任何额外的资金。一旦我们完成了检查，我们就可以存储承诺（commitment），发出`Commit``事件，并进入下一个阶段。

```d
function commit(bytes32 commitment) public payable {
    // Only run during commit stages
    uint playerIndex;
    if(stage == Stage.FirstCommit) playerIndex = 0;
    else if(stage == Stage.SecondCommit) playerIndex = 1;
    else revert("both players have already played");

    uint commitAmount = bet + deposit;
    require(commitAmount >= bet, "overflow error");
    require(msg.value >= commitAmount, "value must be greater than commit amount");

    // Return additional funds transferred
    if(msg.value > commitAmount) {
        (bool success, ) = msg.sender.call.value(msg.value - commitAmount)("");
        require(success, "call failed");
    }

    // Store the commitment
    players[playerIndex] = CommitChoice(msg.sender, commitment, Choice.None);

    // Emit the commit event
    emit Commit(msg.sender);

    // If we're on the first commit, then move to the second
    if(stage == Stage.FirstCommit) stage = Stage.SecondCommit;
    // Otherwise we must already be on the second, move to first reveal
    else stage = Stage.FirstReveal;
}
```

现在让我们继续讨论揭示（reveal）函数，同样从检查开始。我们将`require`这个函数只在reveal阶段之一运行，并且只接受有效的选择。然后我们将找到玩家的数据，这样我们就可以检查他们的承诺哈希值（commitment hash）以确定它是否有效。如果他们的哈希值是有效的，我们将存储它，触发`Reveal`事件，并进入下一个阶段。

```d
function reveal(Choice choice, bytes32 blindingFactor) public {
    // Only run during reveal stages
    require(stage == Stage.FirstReveal || stage == Stage.SecondReveal, "not at reveal stage");
    // Only accept valid choices
    require(choice == Choice.Rock || choice == Choice.Paper || choice == Choice.Scissors, "invalid choice");

    // Find the player index
    uint playerIndex;
    if(players[0].playerAddress == msg.sender) playerIndex = 0;
    else if (players[1].playerAddress == msg.sender) playerIndex = 1;
    // Revert if unknown player
    else revert("unknown player");

    // Find player data
    CommitChoice storage commitChoice = players[playerIndex];

    // Check the hash to ensure the commitment is correct
    require(keccak256(abi.encodePacked(msg.sender, choice, blindingFactor)) == commitChoice.commitment, "invalid hash");

    // Update choice if correct
    commitChoice.choice = choice;

    // Emit reveal event
    emit Reveal(msg.sender, commitChoice.choice);

    if(stage == Stage.FirstReveal) {
        // If this is the first reveal, set the deadline for the second one
        revealDeadline = block.number + revealSpan;
        require(revealDeadline >= block.number, "overflow error");
        // Move to second reveal
        stage = Stage.SecondReveal;
    }
    // If we're on second reveal, move to distribute stage
    else stage = Stage.Distribute;
}
```

最后，我们有分发(distribute)函数，在这里我们将决定赢家并支付他们钱。像往常一样，我们将要求该函数只在我们处于分发阶段时运行，或者reveal阶段的时间已经用完。接下来，我们将计算赔款，并通过检查所有可能的结果来确定赢家。一旦我们确定了赢家，我们将把资金转移到他们的地址，发出`Payout`事件，并为下一个游戏重置状态。

```d
function distribute() public {
    // To distribute we need:
        // a) To be in the distribute stage OR
        // b) Still in the second reveal stage but past the deadline
    require(stage == Stage.Distribute || (stage == Stage.SecondReveal && revealDeadline <= block.number), "cannot yet distribute");

    // Calculate value of payouts for players
    uint player0Payout;
    uint player1Payout;
    uint winningAmount = deposit + 2 * bet;
    require(winningAmount / deposit == 2 * bet, "overflow error");

    // If both players picked the same choice, return their deposits and bets
    if(players[0].choice == players[1].choice) {
        player0Payout = deposit + bet;
        player1Payout = deposit + bet;
    }
    // If only one player made a choice, they win
    else if(players[0].choice == Choice.None) {
        player1Payout = winningAmount;
    }
    else if(players[1].choice == Choice.None) {
        player0Payout = winningAmount;
    }
    else if(players[0].choice == Choice.Rock) {
        assert(players[1].choice == Choice.Paper || players[1].choice == Choice.Scissors);
        if(players[1].choice == Choice.Paper) {
            // Rock loses to paper
            player0Payout = deposit;
            player1Payout = winningAmount;
        }
        else if(players[1].choice == Choice.Scissors) {
            // Rock beats scissors
            player0Payout = winningAmount;
            player1Payout = deposit;
        }

    }
    else if(players[0].choice == Choice.Paper) {
        assert(players[1].choice == Choice.Rock || players[1].choice == Choice.Scissors);
        if(players[1].choice == Choice.Rock) {
            // Paper beats rock
            player0Payout = winningAmount;
            player1Payout = deposit;
        }
        else if(players[1].choice == Choice.Scissors) {
            // Paper loses to scissors
            player0Payout = deposit;
            player1Payout = winningAmount;
        }
    }
    else if(players[0].choice == Choice.Scissors) {
        assert(players[1].choice == Choice.Paper || players[1].choice == Choice.Rock);
        if(players[1].choice == Choice.Rock) {
            // Scissors lose to rock
            player0Payout = deposit;
            player1Payout = winningAmount;
        }
        else if(players[1].choice == Choice.Paper) {
            // Scissors beats paper
            player0Payout = winningAmount;
            player1Payout = deposit;
        }
    }
    else revert("invalid choice");

    // Send the payouts
    if(player0Payout > 0) {
        (bool success, ) = players[0].playerAddress.call.value(player0Payout)("");
        require(success, 'call failed');
        emit Payout(players[0].playerAddress, player0Payout);
    } else if (player1Payout > 0) {
        (bool success, ) = players[1].playerAddress.call.value(player1Payout)("");
        require(success, 'call failed');
        emit Payout(players[1].playerAddress, player1Payout);
    }

    // Reset the state to play again
    delete players;
    revealDeadline = 0;
    stage = Stage.FirstCommit;
}
```

全部合约源码:

```d
pragma solidity ^0.5.0;

contract RockPaperScissors {
    enum Choice {
        None,
        Rock,
        Paper,
        Scissors
    }

    enum Stage {
        FirstCommit,
        SecondCommit,
        FirstReveal,
        SecondReveal,
        Distribute
    }

    struct CommitChoice {
        address playerAddress;
        bytes32 commitment;
        Choice choice;
    }

    event Payout(address player, uint amount);

    // Initialisation args
    uint public bet;
    uint public deposit;
    uint public revealSpan;

    // State vars
    CommitChoice[2] public players;
    uint public revealDeadline;
    Stage public stage = Stage.FirstCommit;

    constructor(uint _bet, uint _deposit, uint _revealSpan) public {
        bet = _bet;
        deposit = _deposit;
        revealSpan = _revealSpan;
    }

    function commit(bytes32 commitment) public payable {
        // Only run during commit stages
        uint playerIndex;
        if(stage == Stage.FirstCommit) playerIndex = 0;
        else if(stage == Stage.SecondCommit) playerIndex = 1;
        else revert("both players have already played");

        uint commitAmount = bet + deposit;
        require(commitAmount >= bet, "overflow error");
        require(msg.value >= commitAmount, "value must be greater than commit amount");

        // Return additional funds transferred
        if(msg.value > commitAmount) {
            (bool success, ) = msg.sender.call.value(msg.value - commitAmount)("");
            require(success, "call failed");
        }

        // Store the commitment
        players[playerIndex] = CommitChoice(msg.sender, commitment, Choice.None);

        // If we're on the first commit, then move to the second
        if(stage == Stage.FirstCommit) stage = Stage.SecondCommit;
        // Otherwise we must already be on the second, move to first reveal
        else stage = Stage.FirstReveal;
    }

    function reveal(Choice choice, bytes32 blindingFactor) public {
        // Only run during reveal stages
        require(stage == Stage.FirstReveal || stage == Stage.SecondReveal, "not at reveal stage");
        // Only accept valid choices
        require(choice == Choice.Rock || choice == Choice.Paper || choice == Choice.Scissors, "invalid choice");

        // Find the player index
        uint playerIndex;
        if(players[0].playerAddress == msg.sender) playerIndex = 0;
        else if (players[1].playerAddress == msg.sender) playerIndex = 1;
        // Revert if unknown player
        else revert("unknown player");

        // Find player data
        CommitChoice storage commitChoice = players[playerIndex];

        // Check the hash to ensure the commitment is correct
        require(keccak256(abi.encodePacked(msg.sender, choice, blindingFactor)) == commitChoice.commitment, "invalid hash");

        // Update choice if correct
        commitChoice.choice = choice;

        if(stage == Stage.FirstReveal) {
            // If this is the first reveal, set the deadline for the second one
            revealDeadline = block.number + revealSpan;
            require(revealDeadline >= block.number, "overflow error");
            // Move to second reveal
            stage = Stage.SecondReveal;
        }
        // If we're on second reveal, move to distribute stage
        else stage = Stage.Distribute;
    }

    function distribute() public {
        // To distribute we need:
            // a) To be in the distribute stage OR
            // b) Still in the second reveal stage but past the deadline
        require(stage == Stage.Distribute || (stage == Stage.SecondReveal && revealDeadline <= block.number), "cannot yet distribute");

        // Calculate value of payouts for players
        uint player0Payout;
        uint player1Payout;
        uint winningAmount = deposit + 2 * bet;
        require(winningAmount / deposit == 2 * bet, "overflow error");

        // If both players picked the same choice, return their deposits and bets
        if(players[0].choice == players[1].choice) {
            player0Payout = deposit + bet;
            player1Payout = deposit + bet;
        }
        // If only one player made a choice, they win
        else if(players[0].choice == Choice.None) {
            player1Payout = winningAmount;
        }
        else if(players[1].choice == Choice.None) {
            player0Payout = winningAmount;
        }
        else if(players[0].choice == Choice.Rock) {
            assert(players[1].choice == Choice.Paper || players[1].choice == Choice.Scissors);
            if(players[1].choice == Choice.Paper) {
                // Rock loses to paper
                player0Payout = deposit;
                player1Payout = winningAmount;
            }
            else if(players[1].choice == Choice.Scissors) {
                // Rock beats scissors
                player0Payout = winningAmount;
                player1Payout = deposit;
            }

        }
        else if(players[0].choice == Choice.Paper) {
            assert(players[1].choice == Choice.Rock || players[1].choice == Choice.Scissors);
            if(players[1].choice == Choice.Rock) {
                // Paper beats rock
                player0Payout = winningAmount;
                player1Payout = deposit;
            }
            else if(players[1].choice == Choice.Scissors) {
                // Paper loses to scissors
                player0Payout = deposit;
                player1Payout = winningAmount;
            }
        }
        else if(players[0].choice == Choice.Scissors) {
            assert(players[1].choice == Choice.Paper || players[1].choice == Choice.Rock);
            if(players[1].choice == Choice.Rock) {
                // Scissors lose to rock
                player0Payout = deposit;
                player1Payout = winningAmount;
            }
            else if(players[1].choice == Choice.Paper) {
                // Scissors beats paper
                player0Payout = winningAmount;
                player1Payout = deposit;
            }
        }
        else revert("invalid choice");

        // Send the payouts
        if(player0Payout > 0) {
            (bool success, ) = players[0].playerAddress.call.value(player0Payout)("");
            require(success, 'call failed');
            emit Payout(players[0].playerAddress, player0Payout);
        } else if (player1Payout > 0) {
            (bool success, ) = players[1].playerAddress.call.value(player1Payout)("");
            require(success, 'call failed');
            emit Payout(players[1].playerAddress, player1Payout);
        }

        // Reset the state to play again
        delete players;
        revealDeadline = 0;
        stage = Stage.FirstCommit;
    }
}
```


## 参考

- [collision resistance - Simple commitment scheme using secure hash function - Cryptography Stack Exchange](https://crypto.stackexchange.com/questions/72357/simple-commitment-scheme-using-secure-hash-function)
- [steganography - Transfer and hide ciphertext with hash functions? - Cryptography Stack Exchange](https://crypto.stackexchange.com/questions/72278/transfer-and-hide-ciphertext-with-hash-functions)
- [Solidity by Example — Solidity 0.5.3 documentation (soliditylang.org)](https://docs.soliditylang.org/en/v0.5.3/solidity-by-example.html#id2)
- [Commit Reveal Scheme on Ethereum (gitcoin.co)](https://go.gitcoin.co/blog/commit-reveal-scheme-on-ethereum)

